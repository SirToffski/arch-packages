pkgname=libqalculate+clang
pkgver=3.21.0
pkgrel=2
pkgdesc='Multi-purpose desktop calculator'
arch=(x86_64)
url='https://qalculate.github.io/'
license=(GPL)
provides=('libqalculate' "libqalculate=${pkgver}")
conflicts=('libqalculate')
depends=(libxml2 curl mpfr)
makedepends=(intltool doxygen)
optdepends=('gnuplot: for plotting support')
source=("https://github.com/Qalculate/libqalculate/releases/download/v${pkgver}/libqalculate-${pkgver}.tar.gz")
sha256sums=('2a2b6f8de4b43acdff98efdda338436db1a3f7ecd994e1bc2a422a65fba03479')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libqalculate-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libqalculate-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr

  make

}

package() {

  cd "${srcdir:?}/libqalculate-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libqalculate-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

}
