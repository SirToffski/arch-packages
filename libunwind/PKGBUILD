pkgname=libunwind+clang
pkgver=1.5.0
pkgrel=1
pkgdesc='Portable and efficient C programming interface (API) to determine the call-chain of a program'
arch=('x86_64')
url='https://www.nongnu.org/libunwind/'
provides=(libunwind "libunwind=${pkgver}")
conflicts=(libunwind)
license=('GPL')
depends=('xz' 'zlib')
makedepends=('texlive-core')
source=("https://download.savannah.gnu.org/releases/libunwind/libunwind-${pkgver}.tar.gz"{,.sig})
sha512sums=('1df20ca7a8cee2f2e61294fa9b677e88fec52e9d5a329f88d05c2671c69fa462f6c18808c97ca9ff664ef57292537a844f00b18d142b1938c9da701ca95a4bab'
  'SKIP')
validpgpkeys=(
  '5C96BDEAF5F47FB02BD4F6B965D98560914F3F48' # Arun Sharma
  '1675C8DA2EF907FB116EB709EC52B396E6874AF2' # Dave Watson
  '75D2CFC56CC2E935A4143297015A268A17D55FA4' # Dave Watson
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libunwind-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libunwind-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" \
      ./configure --prefix=/usr

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" make

  else

    ./configure --prefix=/usr
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make

  fi

}

check() {

  cd "${srcdir:?}/libunwind-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libunwind-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  # This function is ``supposed'' to fail. Upstream know, but haven't fixed it.
  make check || :

}

package() {

  cd "${srcdir:?}/libunwind-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libunwind-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
}
