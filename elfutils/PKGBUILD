pkgbase=elfutils+clang
pkgname=('debuginfod+clang' 'elfutils+clang' 'libelf+clang')
pkgver=0.185
pkgrel=1
pkgdesc='Handle ELF object files and DWARF debugging information'
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL3')
makedepends=('bzip2' 'curl' 'gcc-libs' 'libarchive' 'libmicrohttpd' 'sqlite'
  'xz' 'zlib' 'zstd')
options=('staticlibs')
source=(
  "https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2"{,.sig}
  clang.patch
)
sha256sums=('dc8d3e74ab209465e7f568e1b3bb9a5a142f8656e2b57d10049a73da2ae6b5a6'
            'SKIP'
            '674f0d2790ff29e0a477eceb9f62ff8fb287ecc3294d430215af349b5f26a014')
validpgpkeys=(
  '47CC0331081B8BC6D0FD4DA08370665B57816A6A' # Mark J. Wielaard <mark@klomp.org>
  'EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A' # Mark Wielaard <mjw@gnu.org>
)

_llvm=0

prepare() {

  cd "${srcdir:?}/elfutils-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/elfutils-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
    patch --forward --strip=1 --input="${srcdir:?}/clang.patch"
  fi

  autoreconf -fiv

}

build() {

  cd "${srcdir:?}/elfutils-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/elfutils-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    sed -i -e 's/ARGP_PROGRAM_BUG_ADDRESS_DEF\s=\sPACKAGE_BUGREPORT;//' debuginfod/debuginfod.cxx
    CFLAGS="${CFLAGS} -Wno-xor-used-as-pow -Wno-gnu-variable-sized-type-not-at-end"
    CXXFLAGS="${CXXFLAGS} -Wno-xor-used-as-pow -Wno-gnu-variable-sized-type-not-at-end"

  fi

  CFLAGS="${CFLAGS} -g" # required for test-suite success

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --program-prefix="eu-" \
    --enable-deterministic-archives

  make

}

check() {

  cd "${srcdir:?}/elfutils-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/elfutils-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  # Some tests fail due to limited capabilities of build chroot
  make check || echo "Known problem: The build environment might lack enough resources."

}

package_libelf+clang() {

  pkgdesc+=" (libraries)"
  depends=('gcc-libs' 'libbz2.so' 'libcurl.so' 'xz' 'zlib' 'zstd')
  # NOTE: the shared objects can not be added to provides as they are not versioned
  provides=("libelf" "libelf=${pkgver}")
  conflicts=("libelf")

  cd "${srcdir:?}/elfutils-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/elfutils-${pkgver} directory! Package libelf Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  # remove files provided by elfutils or debuginfod
  rm -rvf "${pkgdir:?}"{/etc,/usr/{bin,share}}
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,NOTES,README} \
    -t "${pkgdir:?}/usr/share/doc/${pkgname:?}"

}

package_elfutils+clang() {

  pkgdesc+=" (utilities)"
  depends=('gcc-libs' 'glibc' "libelf+clang=${pkgver}-${pkgrel}")
  provides=("elfutils" "elfutils=${pkgver}")
  conflicts=("elfutils")

  cd "${srcdir:?}/elfutils-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/elfutils-${pkgver} directory! Package elfutils Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  install -vDm 644 {AUTHORS,ChangeLog,NEWS,NOTES,README} \
    -t "${pkgdir:?}/usr/share/doc/${pkgname:?}"
  # remove files provided by libelf
  rm -rvf "${pkgdir:?}/usr/"{include,lib}
  # remove files provided by debuginfod
  rm -rvf "${pkgdir:?}/usr/bin/debuginfod"* \
    "${pkgdir:?}/usr/share/man/man1/debuginfod"* \
    "${pkgdir:?}/usr/share/man/man3/debuginfod"* \
    "${pkgdir:?}/usr/share/man/man8/" \
    "${pkgdir:?}/etc"

}

package_debuginfod+clang() {

  pkgdesc+=" (debuginfod)"
  depends=('gcc-libs' 'glibc' 'libarchive.so' "libelf=${pkgver}-${pkgrel}"
    'libmicrohttpd.so' 'sqlite')
  optdepends=("elfutils+clang=${pkgver}-${pkgrel}: for translations")
  provides=("debuginfod" "debuginfod=${pkgver}")
  conflicts=("debuginfod")

  cd "${srcdir:?}/elfutils-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/elfutils-${pkgver} directory! Package debuginfod Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,NOTES,README} \
    -t "${pkgdir:?}/usr/share/doc/${pkgname:?}"
  # remove files provided by libelf
  rm -rvf "${pkgdir:?}/usr/"{include,lib}
  # remove files provided by elfutils
  rm -rvf "${pkgdir:?}/usr/bin/eu-"* \
    "${pkgdir:?}/usr/share/man/man1/eu-"* \
    "${pkgdir:?}/usr/share/man/man3/elf_"* \
    "${pkgdir:?}/usr/share/locale"

}
