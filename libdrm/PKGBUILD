pkgname=libdrm+clang
pkgver=2.4.108
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
url="https://dri.freedesktop.org/"
arch=(x86_64)
license=('custom')
provides=('libdrm' "libdrm=${pkgver}")
conflicts=('libdrm')
depends=('libpciaccess')
makedepends=('valgrind' 'python-docutils' 'meson')
checkdepends=('cairo')
replaces=('libdrm-new' 'libdrm-nouveau')
source=("https://dri.freedesktop.org/libdrm/libdrm-${pkgver}.tar.xz"
  COPYING)
sha512sums=('6a841dc3eb8eae7d75e1d35d23a14d51aff758e5a251dbcf6fc8c28d973d935a4a2f6751b405e956b2777d93c651721199ac80c3cd14b87d021668120d6bc974'
  'b0ca349b882a4326b19f81f22804fabdb6fb7aef31cdc7b16b0a7ae191bfbb50c7daddb2fc4e6c33f1136af06d060a273de36f6f3412ea326f16fa4309fda660')
validpgpkeys=(
  '10A6D91DA1B05BD29F6DEBAC0C74F35979C486BE' # David Airlie <airlied@redhat.com>
  '34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48' # emersion <contact@emersion.fr>
  'E9E246A89E65374EB8355796B8B5FA0EFC1EC2FE' # "Leo Liu <leo.liu@amd.com>"
  '899A810900B815807742E6D895A677A230AC4AA9' # Bas Nieuwenhuizen
)

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

build() {

  meson setup libdrm-${pkgver} build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -Dudev=false \
    -Dvalgrind=false

  ninja -C build

}

check() {

  meson test -C build

}

package() {

  DESTDIR="${pkgdir:?}" meson install -C build
  install -Dt "${pkgdir:?}/usr/share/licenses/${pkgname}" -m644 COPYING

}
