pkgname=krita+clang
pkgver=4.4.8
pkgrel=5
pkgdesc='Edit and paint images'
arch=(x86_64)
url='https://krita.org'
license=(GPL3)
provides=('krita' "krita=${pkgver}")
conflicts=('krita')
depends=(kitemviews kitemmodels ki18n kcompletion kguiaddons kcrash qt5-svg qt5-multimedia quazip
  gsl libraw exiv2 openexr fftw giflib openjpeg2 opencolorio hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost eigen vc poppler-qt5 python-pyqt5 libheif
  qt5-tools sip4 python-sip4 kseexpr git)
optdepends=('poppler-qt5: PDF filter' 'ffmpeg: to save animations'
  "krita-plugin-gmic: G'MIC plugin" 'python-pyqt5: for the Python plugins' 'libheif: HEIF filter'
  'kseexpr: SeExpr generator layer' 'kimageformats: PSD support')
_gitcommit="ec5bae0dad3ccad0c398459126203a87d965c5e0"
source=(
  "krita::git+https://invent.kde.org/graphics/krita.git#commit=${_gitcommit}"
  krita-OpenColorIO2.patch
)
sha256sums=('SKIP'
  'ca24ada0e2bd620e8abe74eff7adda3526925a9b61a968849d3885084bc13816')
validpgpkeys=(
  '05D00A8B73A686789E0A156858B9596C722EA3BD' # Boudewijn Rempt <foundation@krita.org>
  'E9FB29E74ADEACC5E3035B8AB69EB4CF7468332F' # Dmitry Kazakov (main key) <dimula73@gmail.com>
)

_llvm=0

_patch() {

  echo -e "\E[1;33m Apply patch: ${1}\E[0m"
  patch -p1 -i "${srcdir:?}/${1}"

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/krita" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/krita directory! Prepare Failed! \E[0m"
    exit 1
  )

  # Fix build with OpenEXR 3
  git cherry-pick -n d66383f6c593d21463c658fbcc6adef28a7f570f

  # OpenColorIO 2
  _patch krita-OpenColorIO2.patch

  # Remove git hash from version string
  sed -i -e '/get_git_head_hash(GIT_SHA1)/,+12 d' CMakeLists.txt

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    #LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S krita \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DKRITA_DEVS=OFF \
    -DBUILD_KRITA_QT_DESIGNER_PLUGINS=ON \
    -Wno-dev

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
