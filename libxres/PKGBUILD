pkgname=libxres+clang
pkgver=1.2.1
pkgrel=1
pkgdesc="X11 Resource extension library"
arch=('x86_64')
url="https://xorg.freedesktop.org"
license=('custom')
provides=(libxres)
conflicts=(libxres)
depends=('libxext')
makedepends=('xorgproto' 'xorg-util-macros')
source=("${url}/releases/individual/lib/libXres-${pkgver}.tar.bz2"{,.sig})
sha512sums=('c17d28406e17e305dc4976f095e88059735f5bd8c600adf2e556523c9e0dd7fa01dcbf7a9d28a642e77e49a5b30f54bb9b2fc34bfbedce467ee2a020bba5cdf1'
  'SKIP')
validpgpkeys=('C383B778255613DFDB409D91DB221A6900000011') # "Keith Packard <keithp@keithp.com>"

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libXres-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libXres-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" \
      ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --disable-static

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" make

  else

    ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --disable-static

    make

  fi

}

package() {

  cd "${srcdir:?}/libXres-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libXres-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  install -m755 -d "${pkgdir:?}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
