pkgname=texinfo+clang
pkgver=6.8
pkgrel=2
pkgdesc='GNU documentation system for on-line information and printed output'
arch=(x86_64)
url='https://www.gnu.org/software/texinfo/'
license=(GPL3)
provides=(texinfo "texinfo=${pkgver}")
conflicts=(texinfo)
groups=(base-devel)
depends=(ncurses gzip perl sh)
source=(
  "https://ftp.gnu.org/pub/gnu/texinfo/texinfo-${pkgver}.tar.xz"{,.sig}
  texinfo-install.hook
  texinfo-remove.hook
  texinfo-pt-br-crash.patch
)
sha256sums=('8eb753ed28bca21f8f56c1a180362aed789229bd62fff58bf8368e9beb59fec4'
  'SKIP'
  '79925a408f97057d110684b1528251182868704eb357da16f46017b0e974919e'
  '913ca8aac84386399b0a83ed0f6b04b5e6322da62f5c1d7ed31e1050ed37c1a9'
  '73639d69b29ca8274225b16a2a0d51055f1ef76b4a65e9b5b65b5bb0a152f3c7')
validpgpkeys=('EAF669B31E31E1DECBD11513DDBC579DAB37FBA9') # Gavin Smith

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  patch -d texinfo-${pkgver} -p1 -i "${srcdir:?}/texinfo-pt-br-crash.patch" # Avoid info crash for Brazillian Portuguese

}

build() {

  cd "${srcdir:?}/texinfo-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/texinfo-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr

  make

}

check() {

  make check -C "texinfo-${pkgver}"

}

package() {

  make DESTDIR="${pkgdir:?}" install -C "texinfo-${pkgver}"

  install -dm755 "${pkgdir:?}/usr/share/libalpm/hooks/"
  install -m644 texinfo-{install,remove}.hook "${pkgdir:?}/usr/share/libalpm/hooks/"

}
