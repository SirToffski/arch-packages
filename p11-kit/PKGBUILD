pkgbase=p11-kit+clang
pkgname=(p11-kit+clang libp11-kit+clang)
pkgver=0.24.1
pkgrel=1
pkgdesc="Loads and enumerates PKCS#11 modules"
url="https://p11-glue.freedesktop.org"
arch=(x86_64)
license=(BSD)
makedepends=(gtk-doc meson libtasn1 libffi systemd bash-completion)
source=("https://github.com/p11-glue/p11-kit/releases/download/${pkgver}/p11-kit-${pkgver}.tar.xz"{,.sig})
sha256sums=('d8be783efd5cd4ae534cee4132338e3f40f182c3205d23b200094ec85faaaef8'
  'SKIP')
validpgpkeys=(
  'C0F67099B808FB063E2C81117BFB1108D92765AF' # Stef Walter
  '462225C3B46F34879FC8496CD605848ED7E69871' # Daiki Ueno
)

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

build() {

  meson p11-kit-${pkgver} build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D gtk_doc=true \
    -D man=true \
    -D trust_paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source

  meson compile -C build

}

check() {

  meson test -C build --print-errorlogs

}

_pick() {

  local p="$1" f d
  shift
  for f; do
    d="${srcdir:?}/$p/${f#${pkgdir:?}/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done

}

package_p11-kit+clang() {

  provides=('p11-kit' "p11-kit=${pkgver}")
  conflicts=('p11-kit')
  depends=("libp11-kit=${pkgver}-${pkgrel}" coreutils libp11-kit.so libsystemd.so)
  install=p11-kit.install

  meson install -C build --destdir "${pkgdir:?}"
  ln -srf "${pkgdir:?}/usr/bin/update-ca-trust" "${pkgdir:?}/usr/lib/p11-kit/trust-extract-compat"

  # Split libp11-kit
  _pick lib "${pkgdir:?}/usr/include"
  _pick lib "${pkgdir:?}/usr/lib/"{p11-kit-proxy.so,libp11-kit.*}
  _pick lib "${pkgdir:?}/usr/lib/"{pkcs11,pkgconfig}
  _pick lib "${pkgdir:?}/usr/share/"{locale,p11-kit}

  install -Dt "${pkgdir:?}/usr/share/licenses/p11-kit" -m644 "p11-kit-${pkgver}/COPYING"

}

package_libp11-kit+clang() {

  pkgdesc+=" (library)"
  depends=(glibc libtasn1 libffi)
  provides=(libp11-kit.so 'libp11-kit' "libp11-kit=${pkgver}")
  conflicts=('libp11-kit')

  mv lib/* "${pkgdir:?}"

  install -Dt "${pkgdir:?}/usr/share/licenses/libp11-kit" -m644 "p11-kit-${pkgver}/COPYING"

}
