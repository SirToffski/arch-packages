pkgname=jemalloc+clang
epoch=1
pkgver=5.3.0
pkgrel=1
pkgdesc='General-purpose scalable concurrent malloc implementation'
arch=('x86_64')
license=('BSD')
url='http://www.canonware.com/jemalloc/'
depends=('glibc')
provides=('jemalloc' "jemalloc=${pkgver}" 'libjemalloc.so')
conflicts=('jemalloc')
optdepends=('perl: for jeprof')
source=("https://github.com/jemalloc/jemalloc/releases/download/${pkgver}/jemalloc-${pkgver}.tar.bz2")
sha256sums=('2db82d1e7119df3e71b7640219b6dfe84789bc0537983c3b7ac4f7189aecfeaa')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/jemalloc-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/jemalloc-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -std=c++17 -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --with-malloc-conf=/etc/malloc.conf \
    --enable-autogen

  make

}

package() {

  cd "${srcdir:?}/jemalloc-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/jemalloc-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  install -Dm644 COPYING "${pkgdir:?}/usr/share/licenses/$pkgname/COPYING"
  chmod 644 "${pkgdir:?}/usr/lib/libjemalloc_pic.a"

}
