pkgname=zstd+clang
pkgver=1.5.2
pkgrel=7
pkgdesc='Zstandard - Fast real-time compression algorithm'
url='https://facebook.github.io/zstd/'
arch=(x86_64)
license=(BSD GPL2)
depends=(glibc gcc-libs zlib xz lz4)
makedepends=(cmake gtest)
provides=('zstd' "zstd=${pkgver}" 'libzstd.so')
conflicts=('zstd')
source=("https://github.com/facebook/zstd/releases/download/v${pkgver}/zstd-${pkgver}.tar.zst"{,.sig})

sha256sums=('3ea06164971edec7caa2045a1932d757c1815858e4c2b68c7ef812647535c23f'
  'SKIP')

validpgpkeys=(4EF4AC63455FC9F4545D9B7DEF8FE99528B52FFD)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  # avoid error on tests without static libs, we use LD_LIBRARY_PATH
  sed '/build static library to build tests/d' -i "zstd-${pkgver}/build/cmake/CMakeLists.txt"
  sed 's/libzstd_static/libzstd_shared/g' -i "zstd-${pkgver}/build/cmake/tests/CMakeLists.txt"

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -S "zstd-${pkgver}/build/cmake" -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DZSTD_BUILD_CONTRIB=ON \
    -DZSTD_BUILD_STATIC=OFF \
    -DZSTD_BUILD_TESTS=ON \
    -DZSTD_PROGRAMS_LINK_SHARED=ON

  cmake --build build

}

check() {

  LD_LIBRARY_PATH="$(pwd)/zstd-${pkgver}/build/lib" \
    cmake --build build --target test

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build
  ln -sf /usr/bin/zstd "${pkgdir:?}/usr/bin/zstdmt"

  install -Dm 644 "zstd-${pkgver}/LICENSE" -t "${pkgdir:?}/usr/share/licenses/zstd"

}
