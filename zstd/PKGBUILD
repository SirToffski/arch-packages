pkgname=zstd+clang
pkgver=1.5.2
pkgrel=2
pkgdesc='Zstandard - Fast real-time compression algorithm'
url='https://facebook.github.io/zstd/'
arch=(x86_64)
license=(BSD GPL2)
depends=(glibc gcc-libs zlib xz lz4)
makedepends=(gtest)
provides=('zstd' "zstd=${pkgver}" 'libzstd.so')
conflicts=('zstd')
source=("https://github.com/facebook/zstd/releases/download/v${pkgver}/zstd-${pkgver}.tar.zst"{,.sig})

sha256sums=('3ea06164971edec7caa2045a1932d757c1815858e4c2b68c7ef812647535c23f'
  'SKIP')

validpgpkeys=(4EF4AC63455FC9F4545D9B7DEF8FE99528B52FFD)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/zstd-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/zstd-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  make
  make -C contrib/pzstd

}

check() {

  cd "${srcdir:?}/zstd-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/zstd-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  make check
  make -C contrib/pzstd test

}

package() {

  cd "${srcdir:?}/zstd-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/zstd-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" PREFIX=/usr install
  ln -sf /usr/bin/zstd "${pkgdir:?}/usr/bin/zstdmt"
  install -Dm 755 contrib/pzstd/pzstd -t "${pkgdir:?}/usr/bin"
  install -Dm 644 LICENSE -t "${pkgdir:?}/usr/share/licenses/${pkgname}"

}
