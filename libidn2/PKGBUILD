pkgname=libidn2+clang
pkgver=2.3.2
pkgrel=1
pkgdesc="Free software implementation of IDNA2008, Punycode and TR46"
arch=('x86_64')
url="https://www.gnu.org/software/libidn/#libidn2"
license=('GPL2' 'LGPL3')
provides=("libidn2" "libidn2=${pkgver}" 'libidn2.so')
conflicts=("libidn2")
depends=('libunistring')
makedepends=('ruby-ronn')
source=("https://ftp.gnu.org/gnu/libidn/libidn2-${pkgver}.tar.gz"{,.sig})
sha256sums=('76940cd4e778e8093579a9d195b25fff5e936e9dc6242068528b437a76764f91'
  'SKIP')
validpgpkeys=('9AA9BDB11BB1B99A21285A330664A76954265E8C' # Simon Josefsson <simon@josefsson.org>
  '1CB27DBC98614B2D5841646D08302DB6A2670428')            # Tim RÃ¼hsen <tim.ruehsen@gmx.de>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libidn2-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libidn2-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
    CFLAGS="${CFLAGS} -flto=thin -fuse-ld=lld -Wno-unused-command-line-argument"
    CXXFLAGS="${CXXFLAGS} -flto=thin -fuse-ld=lld -Wno-unused-command-line-argument"
    LDFLAGS="${LDFLAGS} -flto=thin"

  fi

  ./configure \
    --prefix=/usr \
    --disable-static

  make

}

check() {

  make -C "libidn2-${pkgver}" check

}
package() {

  make -C "libidn2-${pkgver}" DESTDIR="${pkgdir:?}" install
}
