pkgname=zita-resampler+clang
pkgver=1.8.0
pkgrel=1
pkgdesc="A C++ library for resampling audio signals"
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
arch=('x86_64')
license=('GPL3')
depends=('gcc-libs' 'glibc')
makedepends=('libsndfile')
optdepends=('libsndfile: for zresample and zretune')
provides=('zita-resampler' "zita-resampler=${pkgver}" 'libzita-resampler.so')
conflicts=('zita-resampler')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/zita-resampler-${pkgver}.tar.bz2")
sha512sums=('adf1f465ac09104be5a02c1cf0f052a44b4c1a448312f3aca016a889e98cfcdebfded38f60300ad85d18ae03eadca7e8e95efb2875267bc36435ccfac881a8e7')
b2sums=('1ba5d255fff9c66e18e6177bc7e48a4710fdf54f58c07f162f8c8801f4f449b22dea9922645e7e33b70e07739c2b381176714e704f9840305f194f3c23d66de2')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/zita-resampler-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/zita-resampler-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  # FS#36248 - strip march=native
  sed -e '/native/d' \
    -i {source,apps}/Makefile
  # removing local call to ldconfig
  sed -e 's/ldconfig/& -N $(DESTDIR)\/$(LIBDIR)/' \
    -i source/Makefile
  # fix use of BINDIR without preceeding DESTDIR
  # fix use of MANDIR without preceeding DESTDIR
  sed -e 's/\ $(BINDIR)/\ $(DESTDIR)$(BINDIR)/' \
    -e 's/\ $(MANDIR)/\ $(DESTDIR)$(MANDIR)/' \
    -i apps/Makefile

}

build() {

  cd "${srcdir:?}/zita-resampler-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/zita-resampler-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    # libs
    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" \
      make LDFLAGS="-flto=thin ${LDFLAGS}" PREFIX="/usr" -C source
    # create lib link for building apps
    ln -sv "libzita-resampler.so.${pkgver}" "source/libzita-resampler.so"
    # apps
    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin -I../source" \
      make PREFIX="/usr" LDFLAGS="-flto=thin ${LDFLAGS} -L../source" -C apps

  else

    # libs
    make -C source
    # create lib link for building apps
    ln -sv "libzita-resampler.so.${pkgver}" "source/libzita-resampler.so"
    # apps
    make LDFLAGS+=" -L../source" CXXFLAGS+=" -I../source" -C apps

  fi

  # cleanup link
  rm -rv "source/libzita-resampler.so"

}

package() {

  cd "${srcdir:?}/zita-resampler-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/zita-resampler-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make -C source DESTDIR="${pkgdir:?}/" PREFIX=/usr SUFFIX="" install
  make -C apps DESTDIR="${pkgdir:?}/" PREFIX=/usr install
  # docs
  install -vDm 644 {AUTHORS,README} docs/* \
    -t "${pkgdir:?}/usr/share/doc/${pkgname}"

}
