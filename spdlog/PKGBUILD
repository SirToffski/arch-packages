pkgname=spdlog+clang
pkgver=1.9.2
pkgrel=3
pkgdesc='Very fast, header-only/compiled, C++ logging library'
arch=('x86_64')
url='https://github.com/gabime/spdlog'
license=('MIT')
makedepends=('cmake')
source=(
  "spdlog-${pkgver}.tar.gz::https://github.com/gabime/spdlog/archive/v${pkgver}.tar.gz"
)
provides=('libspdlog.so' spdlog "spdlog=${pkgver}")
conflicts=(spdlog)
sha256sums=('6fff9215f5cb81760be4cc16d033526d1080427d236e86d70bb02994f85e3d38')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S "spdlog-${pkgver}" \
    -DSPDLOG_BUILD_BENCH=OFF \
    -DSPDLOG_FMT_EXTERNAL=OFF \
    -DSPDLOG_BUILD_SHARED=ON \
    -DSPDLOG_BUILD_TESTS=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build
  install -Dm644 "spdlog-${pkgver}/LICENSE" -t "${pkgdir:?}/usr/share/licenses/spdlog/"

}
