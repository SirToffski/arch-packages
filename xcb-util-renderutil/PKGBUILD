pkgname=xcb-util-renderutil+clang
pkgver=0.3.9
pkgrel=3
pkgdesc="Utility libraries for XC Binding - Convenience functions for the Render extension"
arch=('x86_64')
url="https://xcb.freedesktop.org"
license=('custom')
provides=("xcb-util-renderutil" "xcb-util-renderutil=${pkgver}")
conflicts=("xcb-util-renderutil")
depends=('libxcb>=1.7')
makedepends=('xorg-util-macros' 'xorgproto')
source=("https://xcb.freedesktop.org/dist/xcb-util-renderutil-${pkgver}.tar.bz2"
  'LICENSE')
sha512sums=('24b567992ecde7e69a406efd0ffb62266610d73b4dc0e5c71093221090fa134c14b850680d1d28dc1f8f4a6f497b4118491e1894ec7c73d2c6ce959d106771cc'
  '62f83468f9d9503a5a8c1ebaac78cbaefe3c485f8bfd7192817ec59254872fe2914ed66ae8b9e7f7a6ef5af283048c11390f44eb8f0f70b1c136ef5c75282e8c')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/xcb-util-renderutil-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/xcb-util-renderutil-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --disable-static

  make

}

check() {

  cd "${srcdir:?}/xcb-util-renderutil-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/xcb-util-renderutil-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  make check

}

package() {

  cd "${srcdir:?}/xcb-util-renderutil-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/xcb-util-renderutil-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  install -D -m644 "${srcdir:?}/LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

}
