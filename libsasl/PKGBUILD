pkgname=libsasl+clang
pkgver=2.1.28
pkgrel=1
pkgdesc="Cyrus Simple Authentication Service Layer (SASL) library"
arch=(x86_64)
url="https://www.cyrusimap.org/sasl/"
license=(custom)
depends=(gdbm libgdbm.so glibc openssl)
makedepends=(krb5 libldap mariadb-libs postgresql-libs sqlite)
provides=(libsasl2.so "libsasl" "libsasl=${pkgver}")
conflicts=("libsasl")
source=("https://github.com/cyrusimap/cyrus-sasl/releases/download/cyrus-sasl-${pkgver}/cyrus-sasl-${pkgver}.tar.gz"{,.sig})
sha256sums=('7ccfc6abd01ed67c1a0924b353e526f1b766b21f42d4562ee635a8ebfc5bb38c'
  'SKIP')
validpgpkeys=(
  '829F339F8C296FE80F409D93E3D7C118C7B9F46A' # Partha Susarla <mail@spartha.org>
  'DEA1999F0CDB1AAEBA001E0DBEE3E3B4D2F06546' # Quanah Gibson-Mount <quanah@fast-mail.org>
)

_llvm=0

_patch() {

  echo -e "\E[1;33m Apply patch: ${1}\E[0m"
  patch -p1 -i "${srcdir:?}/${1}"

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/cyrus-sasl-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/cyrus-sasl-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  autoreconf -fiv

}

build() {

  CFLAGS="${CFLAGS} -fPIC"

  cd "${srcdir:?}/cyrus-sasl-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/cyrus-sasl-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then
    CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"
  fi

  ./configure \
    --prefix=/usr \
    --disable-krb4 \
    --disable-macos-framework \
    --disable-otp \
    --disable-passdss \
    --disable-srp \
    --disable-srp-setpass \
    --disable-static \
    --enable-alwaystrue \
    --enable-anon \
    --enable-auth-sasldb \
    --enable-checkapop \
    --enable-cram \
    --enable-digest \
    --enable-gssapi \
    --enable-ldapdb \
    --enable-login \
    --enable-ntlm \
    --enable-plain \
    --enable-shared \
    --enable-sql \
    --infodir=/usr/share/info \
    --mandir=/usr/share/man \
    --sbin=/usr/bin \
    --sysconfdir=/etc \
    --with-dblib=gdbm \
    --with-devrandom=/dev/urandom \
    --with-configdir=/etc/sasl2:/etc/sasl:/usr/lib/sasl2 \
    --with-ldap \
    --with-mysql=/usr \
    --with-pam \
    --with-pgsql=/usr/lib \
    --with-saslauthd=/var/run/saslauthd \
    --with-sqlite3=/usr/lib

  make

}

check() {

  make -k check -C "cyrus-sasl-${pkgver}"

}

package() {

  make DESTDIR="${pkgdir:?}" install-pkgconfigDATA -C "cyrus-sasl-${pkgver}"

  for _target in include lib sasldb plugins utils; do
    make DESTDIR="${pkgdir:?}" install -C "cyrus-sasl-${pkgver}/${_target}"
  done

  install -vDm 644 "cyrus-sasl-${pkgver}/COPYING" -t "${pkgdir:?}/usr/share/licenses/libsasl"

  # remove files provided by extra/cyrus-sasl
  rm -fv "${pkgdir:?}/usr/lib/sasl2/lib"{gs2,gssapiv2,ldapdb,sql}.so*

}
