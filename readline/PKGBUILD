pkgname=readline+clang
_basever=8.1
_patchlevel=002
pkgver=${_basever}.${_patchlevel}
pkgrel=1
pkgdesc='GNU readline library'
arch=('x86_64')
url='https://tiswww.case.edu/php/chet/readline/rltop.html'
license=('GPL')
backup=('etc/inputrc')
depends=('glibc' 'ncurses' 'libncursesw.so')
provides=("readline" "readline=${pkgver}" 'libhistory.so' 'libreadline.so')
conflicts=("readline")
options=('!emptydirs')
source=(
  "https://ftp.gnu.org/gnu/readline/readline-${_basever}.tar.gz"{,.sig}
  inputrc
)
sha256sums=('f8ceb4ee131e3232226a17f51b164afc46cd0b9e6cef344be87c65962cb82b02'
  'SKIP'
  '36e9611f935ee108d161587b0615f9c390192ef4bbff6dc59b58671261029901'
  '682a465a68633650565c43d59f0b8cdf149c13a874682d3c20cb4af6709b9144'
  'SKIP'
  'e55be055a68cb0719b0ccb5edc9a74edcc1d1f689e8a501525b3bc5ebad325dc'
  'SKIP')
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB') # Chet Ramey

_llvm=0

if [ $_patchlevel -gt 0 ]; then
  for ((_p = 1; _p <= $((10#${_patchlevel})); _p++)); do
    source=(${source[@]} https://ftp.gnu.org/gnu/readline/readline-${_basever}-patches/readline${_basever//./}-$(printf "%03d" $_p){,.sig})
  done
fi

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/readline-${_basever}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/readline-${_basever} directory! Prepare Failed! \E[0m"
    exit 1
  )

  for ((_p = 1; _p <= $((10#${_patchlevel})); _p++)); do
    msg "applying patch readline${_basever//./}-$(printf "%03d" $_p)"
    patch -p0 -i ../readline${_basever//./}-$(printf "%03d" $_p)
  done

  # remove RPATH from shared objects (FS#14366)
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf

}

build() {

  cd "${srcdir:?}/readline-${_basever}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/readline-${_basever} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  # build with -fPIC for x86_64 (FS#15634)
  [[ $CARCH == "x86_64" ]] && CFLAGS="$CFLAGS -fPIC"

  ./configure \
    --prefix=/usr

  make SHLIB_LIBS=-lncurses

}

package() {

  make -C "readline-${_basever}" DESTDIR="${pkgdir:?}" install
  install -Dm644 inputrc "${pkgdir:?}/etc/inputrc"

}
