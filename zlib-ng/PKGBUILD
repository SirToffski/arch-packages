pkgname=zlib-ng+clang
pkgver=2.0.5
pkgrel=1
pkgdesc="zlib replacement with optimizations for \"next generation\" systems"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/zlib-ng/zlib-ng"
license=('zlib')
depends=('glibc')
makedepends=('cmake')
provides=('zlib' 'zlib=1.2.11')
conflicts=('zlib')
source=(
  "https://github.com/zlib-ng/zlib-ng/archive/${pkgver//_/-}.zip"
)
sha256sums=('fb3a88f1c03afc9e1792abcfd146978239be80843ddf6067bf299806320757b9')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/zlib-ng-${pkgver//_/-}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/zlib-ng-${pkgver//_/-} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"

  fi

  cmake -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DZLIB_COMPAT=ON \
    -Wno-dev

  make -C build

}

check() {

  cd "${srcdir:?}/zlib-ng-${pkgver//_/-}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/zlib-ng-${pkgver//_/-} directory! Check Failed! \E[0m"
    exit 1
  )

  make -C build test

}

package() {

  cd "${srcdir:?}/zlib-ng-${pkgver//_/-}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/zlib-ng-${pkgver//_/-} directory! Package Failed! \E[0m"
    exit 1
  )

  DESTDIR="${pkgdir:?}" make -C build install
  install -D -m644 "LICENSE.md" "${pkgdir:?}/usr/share/licenses/${pkgname}/LICENSE"

}
