pkgname=zlib-ng+clang
pkgver=2.0.6
pkgrel=1
pkgdesc="zlib replacement with optimizations for \"next generation\" systems"
arch=('x86_64')
url="https://github.com/zlib-ng/zlib-ng"
license=('zlib')
depends=('glibc')
makedepends=('cmake')
provides=('zlib' 'zlib=1.2.11')
conflicts=('zlib')
source=(
  "https://github.com/zlib-ng/zlib-ng/archive/${pkgver//_/-}.zip"
)
sha256sums=('cb8af7677e5f3bd9e56a6b1c384d9c17bf3d44aeb3c0523453f00772185b337a')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S "zlib-ng-${pkgver//_/-}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DZLIB_COMPAT=ON \
    -Wno-dev

  make -C build

}

check() {

  make -C build test

}

package() {

  DESTDIR="${pkgdir:?}" make -C build install
  install -D -m644 "zlib-ng-${pkgver//_/-}/LICENSE.md" "${pkgdir:?}/usr/share/licenses/${pkgname}/LICENSE"

}
