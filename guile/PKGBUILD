pkgname=guile+clang
pkgver=2.2.7
pkgrel=2
pkgdesc='Portable, embeddable Scheme implementation written in C'
url='https://www.gnu.org/software/guile/'
arch=(x86_64)
license=(GPL)
provides=('guile' "guile=${pkgver}")
conflicts=('guile')
depends=(gmp libltdl ncurses texinfo libunistring gc libffi)
source=("https://ftp.gnu.org/pub/gnu/guile/guile-${pkgver}.tar.gz"{,.sig})
validpgpkeys=(
  '3CE464558A84FDC69DB40CFB090B11993D9AEBB5' # Ludovic Court√®s <ludo@gnu.org>
  'FF478FB264DE32EC296725A3DDC0F5358812F8F2' # Andy Wingo
  '4FD4D288D445934E0A14F9A5A8803732E4436885' # Andy Wingo <wingo@pobox.com>"
)
sha256sums=('44b4c5fbbe257ccdebea18420212c9b3e90c3c86a54920d8554039fc6769a007'
  'SKIP')

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/guile-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/guile-${pkgver} build directory! Prepare Failed! \E[0m"
    exit 1
  )

  sed -e '/test-out-of-memory/d' -i test-suite/standalone/Makefile.am # Disable test known to fail https://bugs.debian.org/cgi-bin/bugreport.cgi?bug=966301

  autoreconf -if

}

build() {

  cd "${srcdir:?}/guile-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/guile-${pkgver} build directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-error-on-warning

  make

}

check() {

  cd "${srcdir:?}/guile-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/guile-${pkgver} build directory! Check Failed! \E[0m"
    exit 1
  )

  make check || true

}

package() {

  make -C guile-${pkgver} DESTDIR="${pkgdir:?}" install
  rm "${pkgdir:?}/usr/lib/libguile-2."?.so.*-gdb.scm

}
