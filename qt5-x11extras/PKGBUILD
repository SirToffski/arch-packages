pkgname=qt5-x11extras+clang
_qtver=5.15.2
pkgver=${_qtver/-/}
pkgrel=2
arch=('x86_64')
url='https://www.qt.io'
provides=("qt5-x11extras" "qt5-x11extras=${pkgver}")
conflicts=("qt5-x11extras")
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Provides platform-specific APIs for X11'
depends=('qt5-base')
groups=('qt' 'qt5')
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/qtx11extras-everywhere-src-${_qtver}.tar.xz")
sha256sums=('7014702ee9a644a5a93da70848ac47c18851d4f8ed622b29a72eed9282fc6e3e')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  if [[ ! -d build ]]; then
    mkdir -p build
  fi

}

build() {

  cd "${srcdir:?}/build" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/build directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  qmake ../qtx11extras-everywhere-src-${_qtver}
  make

}

package() {

  cd "${srcdir:?}/build" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/build directory! Package Failed! \E[0m"
    exit 1
  )

  make INSTALL_ROOT="${pkgdir:?}" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir:?}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "${pkgdir:?}"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "${pkgdir:?}/usr/share/licenses/${pkgname}"

}
