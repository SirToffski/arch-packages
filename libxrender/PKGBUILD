pkgname=libxrender+clang
pkgver=0.9.10
pkgrel=5
pkgdesc="X Rendering Extension client library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
provides=("libxrender" "libxrender=${pkgver}")
conflicts=("libxrender")
depends=('libx11')
makedepends=('xorgproto')
source=("https://xorg.freedesktop.org/releases/individual/lib/libXrender-${pkgver}.tar.bz2"{,.sig})
sha256sums=('c06d5979f86e64cabbde57c223938db0b939dff49fdb5a793a1d3d0396650949'
  'SKIP')
validpgpkeys=(
  '4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E' #Alan Coopersmith <alan.coopersmith@oracle.com>
  'C41C985FDCF1E5364576638B687393EE37D128F8' # Matthieu Herrb <matthieu.herrb@laas.fr>
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libXrender-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/libXrender-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --disable-static

  make

}

package() {

  cd "${srcdir:?}/libXrender-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/libXrender-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  install -Dm644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/COPYING"

}
