pkgname=alsa-lib+clang
pkgver=1.2.7
pkgrel=2
pkgdesc="An alternative implementation of Linux sound support"
arch=('x86_64')
url="https://www.alsa-project.org"
license=('LGPL2.1')
depends=('glibc' 'alsa-topology-conf' 'alsa-ucm-conf')
provides=('alsa-lib' "alsa-lib=${pkgver}" 'libasound.so' 'libatopology.so')
conflicts=('alsa-lib')
install="alsa-lib.install"
source=(
  "https://www.alsa-project.org/files/pub/lib/alsa-lib-${pkgver}.tar.bz2"{,.sig}
  "alsa-lib-1.2.7-fix_decoding_for_older_hardware.patch::https://github.com/alsa-project/alsa-lib/commit/89ee61914756a6f8bcafbad7fb1eca674b0a012f.patch"
)
sha256sums=('8814e61f7ec6812c76e23a85cab00e0b0d3bba40816af36b726beb1bc04c74a7'
  'SKIP'
  'a6a9aa8b02f462c73a9db550a427ae62882a41fe73a0c4fb7f0f9bac53b18aa8')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/alsa-lib-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/alsa-lib-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  # Fix issues with older hardware: https://github.com/alsa-project/alsa-lib/issues/233
  patch -Np1 -i "${srcdir:?}/alsa-lib-1.2.7-fix_decoding_for_older_hardware.patch"

  autoreconf -vfi

}

build() {

  cd "${srcdir:?}/alsa-lib-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/alsa-lib-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --without-debug

  make

}

check() {

  cd "${srcdir:?}/alsa-lib-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/alsa-lib-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  export LD_LIBRARY_PATH="${PWD}/src/.libs/:${LD_LIBRARY_PATH}"
  make -k check

}

package() {

  cd "${srcdir:?}/alsa-lib-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/alsa-lib-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  make DESTDIR="${pkgdir:?}" install -C doc
  install -vDm 644 {MEMORY-LEAK,TODO,NOTES,ChangeLog,doc/asoundrc.txt} \
    -t "${pkgdir:?}/usr/share/doc/${pkgname}"

}
