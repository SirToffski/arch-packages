pkgname=kwin+clang
pkgver=5.23.0
pkgrel=2
pkgdesc='An easy to use, but flexible, composited Window Manager'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
provides=('kwin' "kwin=${pkgver}")
conflicts=('kwin')
depends=(kscreenlocker xcb-util-cursor plasma-framework kcmutils kwayland-server breeze
  pipewire-media-session libqaccessibilityclient lcms2)
makedepends=(extra-cmake-modules qt5-tools kdoctools krunner)
optdepends=('maliit-keyboard: virtual keyboard for kwin-wayland')
groups=(plasma)
source=(
  "https://download.kde.org/stable/plasma/${pkgver}/kwin-${pkgver}.tar.xz"{,.sig}
  https://invent.kde.org/plasma/kwin/-/commit/85f8a533.patch
)
install=kwin.install
sha256sums=('ba74f70648faa3c92c88e1b77916c6abd7fcad1af9e717a39dc9df278f1a551f'
            'SKIP'
            '2c42a47dd1d62cde93d3d392d42056689d8fdbc24cd8497bd29adf113e16a7be')
validpgpkeys=(
  'E0A3EB202F8E57528E13E72FD7574483BB57B18D' # Jonathan Riddell <jr@jriddell.org>
  '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D' # Bhushan Shah <bshah@kde.org>
  'D07BD8662C56CB291B316EB2F5675605C74E02CF' # David Edmundson <davidedmundson@kde.org>
  '1FA881591C26B276D7A5518EEAAF29B42A678C20' # Marco Martin <notmart@gmail.com>
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  patch -d kwin-${pkgver} -p1 <85f8a533.patch # Properly unredirect windows if compositing is not possible

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S kwin-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
