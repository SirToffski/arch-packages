pkgname=libsepol+clang
pkgver=3.3
pkgrel=1
pkgdesc="SELinux binary policy manipulation library"
arch=('x86_64')
url='https://github.com/SELinuxProject/selinux'
license=('LGPL2.1')
groups=('selinux')
makedepends=('flex')
depends=('glibc')
options=(staticlibs)
conflicts=("selinux-usr-libsepol" "libsepol")
provides=("libsepol=${pkgver}")
source=("https://github.com/SELinuxProject/selinux/releases/download/${pkgver}/libsepol-${pkgver}.tar.gz")
sha256sums=('2d97df3eb8466169b389c3660acbb90c54200ac96e452eca9f41a9639f4f238b')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  make -C "libsepol-${pkgver}"

}

package() {

  DESTDIR="${pkgdir:?}" make -C "libsepol-${pkgver}" SHLIBDIR=/usr/lib install

}
