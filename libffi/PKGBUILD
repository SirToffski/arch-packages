pkgname=libffi+clang
pkgver=3.3
pkgrel=4
pkgdesc='Portable foreign function interface library'
arch=('x86_64')
url='https://sourceware.org/libffi/'
license=('MIT')
depends=('glibc')
checkdepends=('dejagnu')
provides=(libffi "libffi=${pkgver}" 'libffi.so')
conflicts=(libffi)
source=("https://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz")
sha256sums=('72fba7922703ddfa7a028d513ac15a85c8d54c8d67f55fa5a4802885dc652056')
b2sums=('cddc40729a30a9bd34d675809f51f8d1b4ccaffa54bc6dd6f7e965f4e260edd34754719f9f6247c8957aeb7cf154d56ce1fe16a54c3f1ad39afbebdf41d23caa')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/libffi-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libffi-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  autoreconf -fiv

}

build() {

  cd "${srcdir:?}/libffi-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libffi-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" \
      ./configure \
      --prefix=/usr \
      --disable-static \
      --enable-pax_emutramp

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" make

  else

    ./configure \
      --prefix=/usr \
      --disable-static \
      --enable-pax_emutramp

    make

  fi

}

check() {

  make -C "libffi-${pkgver}" check

}

package() {

  cd "${srcdir:?}/libffi-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libffi-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  install -Dm 644 LICENSE -t "${pkgdir:?}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir:?}/usr/share/doc/${pkgname}"

}
