pkgname=qtcreator+clang
pkgver=7.0.1
_clangver=14.0.1
pkgrel=1
pkgdesc='Lightweight, cross-platform integrated development environment'
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3)
provides=("qtcreator" "qtcreator=${pkgver}")
conflicts=("qtcreator")
depends=(qt6-tools qt6-svg qt6-quick3d qt6-webengine qt6-serialport qt6-shadertools qt6-5compat
  "clang=${_clangver}" clazy yaml-cpp) # syntax-highlighting
makedepends=(cmake llvm python)
options=(docs)
optdepends=(
  'qt6-doc: integrated Qt documentation'
  'qt6-examples: welcome page examples'
  'qt6-translations: for other languages'
  'gdb: debugger'
  'cmake: cmake project support'
  'x11-ssh-askpass: ssh support'
  'git: git support'
  'mercurial: mercurial support'
  'bzr: bazaar support'
  'valgrind: analyze support'
  'perf: performer analyzer'
  'mlocate: locator filter'
)
source=("https://ftp.fau.de/qtproject/official_releases/qtcreator/${pkgver%.*}/${pkgver}/qt-creator-opensource-src-${pkgver}.tar.xz")
sha256sums=('da78f293829851e32f6bf25540791697606809e47a32ca8439561fcbab96b22e')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -fPIC -flto=thin"
    CXXFLAGS="${CXXFLAGS} -fPIC -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S "qt-creator-opensource-src-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DWITH_DOCS=ON \
    -DBUILD_DEVELOPER_DOCS=ON \
    -DBUILD_QBS=OFF \
    -DQTC_CLANG_BUILDMODE_MATCH=ON

  cmake --build build

  cmake --build build --target docs

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

  # Install docs
  cp -r build/share/doc "${pkgdir:?}/usr/share"

  install -Dm644 "qt-creator-opensource-src-${pkgver}/LICENSE.GPL3-EXCEPT" "${pkgdir:?}/usr/share/licenses/qtcreator/LICENSE.GPL3-EXCEPT"

}
