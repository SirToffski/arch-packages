pkgname=amdvlk+clang
pkgver=2022.Q2.2
pkgrel=1
pkgdesc="AMD's standalone Vulkan driver"
arch=(x86_64)
url="https://github.com/GPUOpen-Drivers"
license=('MIT')
provides=('vulkan-driver' 'amdvlk' "amdvlk=${pkgver}")
conflicts=('amdvlk')
makedepends=('python' 'wayland' 'libxrandr' 'xorg-server-devel'
  'cmake' 'git' 'clang' 'lld' 'glslang' 'spirv-tools'
  'spirv-headers')
# Please use a script generate-source.py for reduce git requests
source=(
  "amdvlk/xgl::git+https://github.com/GPUOpen-Drivers/xgl#commit=9478a913fc7d2ceb4b6eae84bb87fa3e64f08521"
  "amdvlk/pal::git+https://github.com/GPUOpen-Drivers/pal#commit=ca98822bbccc5821a18aa78fb76b43e0ff29f8ef"
  "amdvlk/llpc::git+https://github.com/GPUOpen-Drivers/llpc#commit=be276de91f44263a838c35fdd3fa28a1598e7b42"
  "amdvlk/spvgen::git+https://github.com/GPUOpen-Drivers/spvgen#commit=cd629c08af16529587e1a6486f8b9afa0a475636"
  "amdvlk/AMDVLK::git+https://github.com/GPUOpen-Drivers/AMDVLK#commit=d699b1dc79ce6a86e44f44c3ca2c312e78da46e4"
  "amdvlk/llvm-project::git+https://github.com/GPUOpen-Drivers/llvm-project#commit=219f568b0b603b2d9a64a44264db5f64ec5e9802"
  "amdvlk/MetroHash::git+https://github.com/GPUOpen-Drivers/MetroHash#commit=18893fb28601bb9af1154cd1a671a121fff6d8d3"
  "amdvlk/CWPack::git+https://github.com/GPUOpen-Drivers/CWPack#commit=4f8cf0584442a91d829d269158567d7ed926f026"
)
sha256sums=('SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP')

build() {

  CFLAGS="${CFLAGS} -flto=thin"
  CXXFLAGS="${CXXFLAGS} -flto=thin"
  LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  cmake -S xgl -B build \
    -DLLVM_USE_LINKER=lld \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_WAYLAND_SUPPORT=On

  cmake --build build

}

package() {

  install -m755 -d "${pkgdir:?}/usr/lib"
  install -m755 -d "${pkgdir:?}/usr/share/vulkan/icd.d"
  install -m755 -d "${pkgdir:?}/usr/share/vulkan/implicit_layer.d"
  install -m755 -d "${pkgdir:?}/usr/share/licenses"

  install -Dm644 build/icd/amdvlk64.so "${pkgdir:?}/usr/lib/"
  install -Dm644 build/icd/amd_icd64.json "${pkgdir:?}/usr/share/vulkan/icd.d/"
  install -Dm644 build/icd/amd_icd64.json "${pkgdir:?}/usr/share/vulkan/implicit_layer.d/"
  install -Dm644 AMDVLK/LICENSE.txt "${pkgdir:?}/usr/share/licenses/amdvlk"

  sed -i "s#/lib64#/lib#g" "${pkgdir:?}/usr/share/vulkan/icd.d/amd_icd64.json"
  sed -i "s#/lib64#/lib#g" "${pkgdir:?}/usr/share/vulkan/implicit_layer.d/amd_icd64.json"

}
