pkgname=amdvlk+clang
pkgver=2022.Q2.3
pkgrel=1
pkgdesc="AMD's standalone Vulkan driver"
arch=(x86_64)
url="https://github.com/GPUOpen-Drivers"
license=('MIT')
provides=('vulkan-driver' 'amdvlk' "amdvlk=${pkgver}")
conflicts=('amdvlk')
makedepends=('python' 'wayland' 'libxrandr' 'xorg-server-devel'
  'cmake' 'git' 'clang' 'lld' 'glslang' 'spirv-tools'
  'spirv-headers')
# Please use a script generate-source.py for reduce git requests
source=(
  "amdvlk/xgl::git+https://github.com/GPUOpen-Drivers/xgl#commit=a9a73115ee5c2d8c717ea39fc31a9d2ae1de8044"
  "amdvlk/pal::git+https://github.com/GPUOpen-Drivers/pal#commit=a40241a1e6f5970a9313efe686a79c0a429ea5eb"
  "amdvlk/llpc::git+https://github.com/GPUOpen-Drivers/llpc#commit=61aabddf65ae7395ec934bce96fa4b7dd0e8880c"
  "amdvlk/spvgen::git+https://github.com/GPUOpen-Drivers/spvgen#commit=eaa8c1dafbdb8db174b30919fcba424579a8c991"
  "amdvlk/AMDVLK::git+https://github.com/GPUOpen-Drivers/AMDVLK#commit=cab8f8631d99240a6503872083bd544fe85f628f"
  "amdvlk/llvm-project::git+https://github.com/GPUOpen-Drivers/llvm-project#commit=ff4dc0eccd74424c9352f8d8ee80f3b4913df35f"
  "amdvlk/MetroHash::git+https://github.com/GPUOpen-Drivers/MetroHash#commit=18893fb28601bb9af1154cd1a671a121fff6d8d3"
  "amdvlk/CWPack::git+https://github.com/GPUOpen-Drivers/CWPack#commit=4f8cf0584442a91d829d269158567d7ed926f026"
)
sha256sums=('SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP')

build() {

  CFLAGS="${CFLAGS} -flto=thin"
  CXXFLAGS="${CXXFLAGS} -flto=thin"
  LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  sed -i "/<unistd.h>/I a #include <time.h>\n" pal/src/util/lnx/lnxUuid.cpp

  cmake -S xgl -B build \
    -DLLVM_USE_LINKER=lld \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_WAYLAND_SUPPORT=On

  cmake --build build

}

package() {

  install -m755 -d "${pkgdir:?}/usr/lib"
  install -m755 -d "${pkgdir:?}/usr/share/vulkan/icd.d"
  install -m755 -d "${pkgdir:?}/usr/share/vulkan/implicit_layer.d"
  install -m755 -d "${pkgdir:?}/usr/share/licenses"

  install -Dm644 build/icd/amdvlk64.so "${pkgdir:?}/usr/lib/"
  install -Dm644 build/icd/amd_icd64.json "${pkgdir:?}/usr/share/vulkan/icd.d/"
  install -Dm644 build/icd/amd_icd64.json "${pkgdir:?}/usr/share/vulkan/implicit_layer.d/"
  install -Dm644 AMDVLK/LICENSE.txt "${pkgdir:?}/usr/share/licenses/amdvlk"

  sed -i "s#/lib64#/lib#g" "${pkgdir:?}/usr/share/vulkan/icd.d/amd_icd64.json"
  sed -i "s#/lib64#/lib#g" "${pkgdir:?}/usr/share/vulkan/implicit_layer.d/amd_icd64.json"

}
