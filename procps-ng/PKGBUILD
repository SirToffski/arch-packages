pkgname=procps-ng+clang
pkgver=3.3.17
pkgrel=1
pkgdesc='Utilities for monitoring your system and its processes'
url='https://gitlab.com/procps-ng/procps'
license=(GPL LGPL)
arch=(x86_64)
depends=(glibc ncurses libncursesw.so systemd-libs)
makedepends=(systemd)
conflicts=(procps sysvinit-tools procps-ng)
provides=(procps sysvinit-tools libprocps.so procps-ng "procps-ng=${pkgver}")
replaces=(procps sysvinit-tools)
options=('!emptydirs')
source=("https://downloads.sourceforge.net/project/procps-ng/Production/procps-ng-${pkgver}.tar.xz"{,.asc})
sha256sums=('4518b3e7aafd34ec07d0063d250fd474999b20b200218c3ae56f5d2113f141b4'
  'SKIP')
validpgpkeys=(
  '5D3DF0F538B327C0AA7A77A2022166C0FF3C84E3' # Craig Small <csmall@debian.org>
  '5D2FB320B825D93904D205193938F96BDF50FEA5' # Craig Small <csmall@debian.org>
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  sed -i 's:<ncursesw/:<:g' "procps-${pkgver}/watch.c"

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cd "${srcdir:?}/procps-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/procps-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  ./configure \
    --prefix=/usr \
    --exec-prefix=/ \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --bindir=/usr/bin \
    --sbindir=/usr/bin \
    --enable-watch8bit \
    --with-systemd \
    --disable-modern-top \
    --disable-kill

  make

}

package() {

  make DESTDIR="${pkgdir:?}" install -C "procps-${pkgver}"

}
