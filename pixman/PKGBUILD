pkgname=pixman+clang
pkgver=0.40.0
pkgrel=1
pkgdesc="The pixel-manipulation library for X and cairo"
arch=(x86_64)
url="https://cgit.freedesktop.org/pixman/"
license=('custom')
depends=('glibc' 'openmp')
makedepends=('meson' 'libpng')
provides=('pixman' "pixman=${pkgver}" 'libpixman-1.so')
conflicts=('pixman')
source=(
  "https://xorg.freedesktop.org/releases/individual/lib/pixman-${pkgver}.tar.xz"
)
sha512sums=(
  '8a60edb113d68791b41bd90b761ff7b3934260cb3dada3234c9351416f61394e4157353bc4d61b8f6c2c619de470f6feefffb4935bfcf79d291ece6285de7270'
)

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

  if [ -d build ]; then
    rm -rf build
  fi

}

build() {

  CFLAGS+=" -ftrapping-math"
  meson setup pixman-${pkgver} build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D loongson-mmi=disabled \
    -D vmx=disabled \
    -D arm-simd=disabled \
    -D neon=disabled \
    -D iwmmxt=disabled \
    -D mips-dspr2=disabled \
    -D gtk=disabled

  ninja -C build

}

check() {

  meson test -C build

}

package() {

  DESTDIR="${pkgdir:?}" meson install -C build
  install -Dt "${pkgdir:?}/usr/share/licenses/${pkgname}" -m644 "pixman-${pkgver}/COPYING"

}
