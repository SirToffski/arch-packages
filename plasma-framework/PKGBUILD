pkgname=plasma-framework+clang
pkgver=5.84.0
pkgrel=1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
provides=(plasma-framework "plasma-framework=${pkgver}")
conflicts=(plasma-framework)
depends=(kactivities kdeclarative kwayland kirigami2)
makedepends=(extra-cmake-modules qt5-tools qt5-doc kdoctools doxygen)

groups=(kf5)
source=(
  "https://download.kde.org/stable/frameworks/${pkgver%.*}/plasma-framework-${pkgver}.tar.xz"{,.sig}
)
sha256sums=('19b5cc632a2e79c28caeea36fdf9e1cc47c1ee1d7c1dc3432378854ac0aa1532'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"

  fi

  cmake -B build -S plasma-framework-${pkgver} \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
