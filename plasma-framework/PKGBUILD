pkgname=plasma-framework+clang
pkgver=5.83.0
pkgrel=1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
provides=(plasma-framework)
conflicts=(plasma-framework)
depends=(kactivities kdeclarative kwayland kirigami2)
makedepends=(extra-cmake-modules qt5-tools qt5-doc kdoctools doxygen)

groups=(kf5)
source=(
  "https://download.kde.org/stable/frameworks/${pkgver%.*}/plasma-framework-${pkgver}.tar.xz"{,.sig}
  "lto.cmake"
)
sha256sums=('7adf5f77d7ecf6a45626e7a329c941f6bfe21154b2ff9c6c943727b0e68f145d'
  'SKIP'
  '7971e5dd106804e615bf270a1d660835ded44f5707263e40a0b402cd1d63348e')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    cmake -B build -S plasma-framework-${pkgver} \
      -DCMAKE_TOOLCHAIN_FILE=../lto.cmake \
      -DBUILD_TESTING=OFF \
      -DBUILD_QCH=ON

  else

    cmake -B build -S plasma-framework-${pkgver} \
      -DBUILD_TESTING=OFF \
      -DBUILD_QCH=ON

  fi

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
