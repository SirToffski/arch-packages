pkgname=graphite+clang
pkgver=1.3.14
pkgrel=1
epoch=1
arch=('x86_64')
url="https://github.com/silnrsi/graphite"
pkgdesc='reimplementation of the SIL Graphite text processing engine'
license=('LGPL' 'GPL' 'custom')
provides=("graphite" "graphite=${pkgver}")
conflicts=("graphite")
depends=('gcc-libs')
makedepends=('cmake' 'freetype2' 'python'
  # for documentation
  'doxygen' 'dblatex' 'graphviz' 'asciidoc')
checkdepends=('python-fonttools')
options=('!emptydirs')
# https://github.com/silnrsi/graphite/releases/download/1.3.14/graphite2-1.3.14.sha256sum
source=("https://github.com/silnrsi/graphite/releases/download/${pkgver}/graphite2-${pkgver}.tgz")
sha256sums=('f99d1c13aa5fa296898a181dff9b82fb25f6cc0933dbaa7a475d8109bd54209d')

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"

  fi

  cmake -B build -S graphite2-${pkgver} \
    -DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE:STRING=Release \
    -DGRAPHITE2_COMPARE_RENDERER=OFF \
    -DGRAPHITE2_VM_TYPE=direct

  # fix unwanted -O3 cflag (taken form Debian)
  find . -type f ! -name "rules" ! -name "changelog" -exec sed -i -e 's/\-O3//g' {} \;

  cmake --build build
  cmake --build build --target docs -- -j1

}

check() {

  cd "${srcdir:?}/build" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/build directory! Check Failed! \E[0m"
    exit 1
  )

  ctest || true

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

  # install doc files
  mkdir -p "${pkgdir:?}/usr/share/doc/graphite2/api"
  cp -vrf "build/doc/doxygen/"{html,latex/refman.pdf} "${pkgdir:?}/usr/share/doc/graphite2/api"
  cp -vrf "build/doc/"{GTF,manual}.html "${pkgdir:?}/usr/share/doc/graphite2"

  # licenses
  mkdir -p "${pkgdir:?}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir:?}/graphite2-${pkgver}/COPYING" "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
