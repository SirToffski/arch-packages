pkgname=zita-alsa-pcmi+clang
pkgver=0.4.0
pkgrel=1
pkgdesc="The successor of clalsadrv, provides easy access to ALSA PCM devices."
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL3')
depends=('gcc-libs' 'glibc' 'libasound.so')
makedepends=('alsa-lib')
provides=('zita-alsa-pcmi' "zita-alsa-pcmi=${pkgver}" 'libzita-alsa-pcmi.so')
conflicts=('zita-alsa-pcmi')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/zita-alsa-pcmi-${pkgver}.tar.bz2")
sha256sums=('bd8c9f360dfb541fb60e48a713baf1e22d41b2c74c19a0fe9f2d34e72f50f1c5')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/zita-alsa-pcmi-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/zita-alsa-pcmi-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  # remove optimization
  # call ldconfig in local context
  sed -e '/native/d' \
    -e 's/ldconfig/& -N $(DESTDIR)\/$(LIBDIR)/' \
    -i source/Makefile
  # fix use of BINDIR without preceeding DESTDIR
  sed -e 's/\ $(BINDIR)/\ $(DESTDIR)$(BINDIR)/' \
    -i apps/Makefile

}

build() {

  cd "${srcdir:?}/zita-alsa-pcmi-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/zita-alsa-pcmi-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  # libs
  make PREFIX="/usr" -C source
  # create lib link for building apps
  ln -sv "libzita-alsa-pcmi.so.${pkgver}" "source/libzita-alsa-pcmi.so"
  CXXFLAGS+=" -I../source" \
    LDFLAGS+=" -L../source" \
    PREFIX="/usr" \
    make -C apps

  # cleanup link
  rm -v source/libzita-alsa-pcmi.so

}

package() {

  cd "${srcdir:?}/zita-alsa-pcmi-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/zita-alsa-pcmi-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  # libs
  make -C source DESTDIR="${pkgdir:?}/" PREFIX=/usr SUFFIX="" install
  install -vDm 644 {AUTHORS,README} -t "${pkgdir:?}/usr/share/doc/${pkgname}"
  # apps
  make -C apps DESTDIR="${pkgdir:?}/" PREFIX=/usr install

}
