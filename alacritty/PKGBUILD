pkgname=alacritty+clang
pkgdesc="A cross-platform, GPU-accelerated terminal emulator"
pkgver=0.10.0
pkgrel=2
arch=('x86_64')
url="https://github.com/alacritty/alacritty"
license=('Apache')
makedepends=('git' 'rust' 'cargo' 'cmake' 'ncurses' 'desktop-file-utils' 'gdb' 'libxcb' 'libxkbcommon')
depends=('freetype2' 'fontconfig' 'libxi' 'libxcursor' 'libxrandr')
conflicts=(alacritty)
checkdepends=('ttf-dejavu') # for monospace fontconfig test
optdepends=('ncurses: for alacritty terminfo database')
_commit=8a26dee0a9167777709935789b95758e36885617
source=(
  "git+https://github.com/alacritty/alacritty.git#commit=${_commit}"
  alacritty-allow-disable-vsync.patch
)
validpgpkeys=(
  '4DAA67A9EA8B91FCC15B699C85CDAE3C164BA7B4' # Christian DÃ¼rr
)
sha256sums=('SKIP'
            'afdb8fa0696c3b28d09461149b0211899b74031e213339b47b76a6dff36169ae')

_llvm=0

_patch() {

  echo -e "\E[1;33m Apply patch: ${1}\E[0m"
  #patch -p1 -i "${srcdir:?}/${1}"

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/alacritty" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/alacritty directory! Prepare Failed! \E[0m"
    exit 1
  )

  # https://bugs.archlinux.org/task/72912
  git cherry-pick -n 58985a4dcbe464230b5d2566ee68e2d34a1788c8

  # On some system vsync is slow. Allow manually disable vsync in alacritty.yml file.
  _patch alacritty-allow-disable-vsync.patch

  cargo fetch --locked --target "x86_64-unknown-linux-gnu"

}

build() {

  cd "${srcdir:?}/alacritty" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/alacritty directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    RUSTFLAGS="${RUSTFLAGS} -C link-arg=-fuse-ld=lld"

  fi

  env CARGO_INCREMENTAL=0 cargo build --release --locked

}

check() {

  cd "${srcdir:?}/alacritty" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/alacritty directory! Check Failed! \E[0m"
    exit 1
  )

  env CARGO_INCREMENTAL=0 cargo test --locked

}

package() {

  cd "${srcdir:?}/alacritty" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/alacritty directory! Package Failed! \E[0m"
    exit 1
  )

  desktop-file-install -m 644 --dir "${pkgdir:?}/usr/share/applications/" "extra/linux/Alacritty.desktop"
  install -D -m755 "target/release/alacritty" "${pkgdir:?}/usr/bin/alacritty"
  install -D -m644 "extra/alacritty.man" "${pkgdir:?}/usr/share/man/man1/alacritty.1"
  install -D -m644 "extra/alacritty-msg.man" "${pkgdir:?}/usr/share/man/man1/alacritty-msg.1"
  install -D -m644 "extra/linux/io.alacritty.Alacritty.appdata.xml" "${pkgdir:?}/usr/share/appdata/io.alacritty.Alacritty.appdata.xml"
  install -D -m644 "alacritty.yml" "${pkgdir:?}/usr/share/doc/alacritty/example/alacritty.yml"
  install -D -m644 "extra/completions/alacritty.bash" "${pkgdir:?}/usr/share/bash-completion/completions/alacritty"
  install -D -m644 "extra/completions/_alacritty" "${pkgdir:?}/usr/share/zsh/site-functions/_alacritty"
  install -D -m644 "extra/completions/alacritty.fish" "${pkgdir:?}/usr/share/fish/vendor_completions.d/alacritty.fish"
  install -D -m644 "extra/logo/alacritty-term.svg" "${pkgdir:?}/usr/share/pixmaps/Alacritty.svg"

}
