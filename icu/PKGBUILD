pkgname=icu+clang
pkgver=69.1
pkgrel=1
pkgdesc="International Components for Unicode library"
arch=(x86_64)
url="http://site.icu-project.org/home"
license=('custom:icu')
depends=('gcc-libs' 'sh')
makedepends=('python')
provides=(icu "icu=${pkgver}" libicu{data,i18n,io,test,tu,uc}.so)
conflicts=(icu)
replaces=(icu)
source=("https://github.com/unicode-org/icu/releases/download/release-${pkgver//./-}/icu4c-${pkgver//./_}-src.tgz"{,.asc})
# https://github.com/unicode-org/icu/releases/download/release-69-1/SHASUM512.txt
sha512sums=('d4aeb781715144ea6e3c6b98df5bbe0490bfa3175221a1d667f3e6851b7bd4a638fa4a37d4a921ccb31f02b5d15a6dded9464d98051964a86f7b1cde0ff0aab7'
  'SKIP')
validpgpkeys=(
  'BA90283A60D67BA0DD910A893932080F4FB419E3' # "Steven R. Loomis (filfla-signing) <srloomis@us.ibm.com>"
  '9731166CD8E23A83BEE7C6D3ACA5DBE1FD8FABF1' # "Steven R. Loomis (ICU Project) <srl@icu-project.org>"
  'FFA9129A180D765B7A5BEA1C9B432B27D1BA20D7' # "Fredrik Roubert <fredrik@roubert.name>"
  'E4098B78AFC94394F3F49AA903996C7C83F12F11' # "keybase.io/srl295 <srl295@keybase.io>"
  '4569BBC09DA846FC91CBD21CE1BBA44593CF2AE0' # "Steven R. Loomis (codesign-qormi) <srloomis@us.ibm.com>"
  '0E51E7F06EF719FBD072782A5F56E5AFA63CCD33' # "Craig Cornelius (For use with ICU releases) <ccornelius@google.com>"
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/icu/source" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/icu/source! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"

  fi

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --sbindir=/usr/bin

  make

}

check() {

  cd "${srcdir:?}/icu/source" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/icu/source! Check Failed! \E[0m"
    exit 1
  )

  make -k check

}

package() {

  cd "${srcdir:?}/icu/source" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/icu/source! Package Failed! \E[0m"
    exit 1
  )

  make -j1 DESTDIR="${pkgdir:?}" install
  install -Dm644 "${srcdir}/icu/LICENSE" "${pkgdir:?}/usr/share/licenses/icu/LICENSE"

}
