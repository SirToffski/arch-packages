pkgbase=alsa-plugins+clang
pkgname=(alsa-plugins+clang pulseaudio-alsa+clang)
pkgver=1.2.6
pkgrel=1
epoch=1
pkgdesc="Additional ALSA plugins"
arch=(x86_64)
url="https://www.alsa-project.org"
license=(LGPL2.1)
makedepends=(alsa-lib dbus jack libavtp libpulse libsamplerate speexdsp)
source=("https://www.alsa-project.org/files/pub/plugins/alsa-plugins-${pkgver}.tar.bz2"{,.sig}
  pulse-sysdefault.diff)
sha256sums=('068818a4b55d8c029daa00015d853d45113f56b224b7c64e1e117988c825b2a0'
  'SKIP'
  'd8c59b891e2913619d057ffea308293d73892b16fa5a01583d056e372e55309d')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/alsa-plugins-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/alsa-plugins-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  # Make use of the pulse plugin's "fallback" feature
  # Keeps parity with our old config from pulseaudio-alsa
  patch -Np1 -i ../pulse-sysdefault.diff

  # a52, lavrate plugins not compatible with ffmpeg >= 4.0
  # https://mailman.alsa-project.org/pipermail/alsa-devel/2019-December/159425.html
  # https://bugs.archlinux.org/task/60586
  autoreconf -fvi

}

build() {

  cd "${srcdir:?}/alsa-plugins-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/alsa-plugins-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-maemo-plugin \
    --enable-maemo-resource-manager

  make

}

package_alsa-plugins+clang() {
  depends=(glibc libasound.so)
  provides=("alsa-plugins" "alsa-plugins=${epoch}:${pkgver}")
  conflicts=("alsa-plugins")
  optdepends=('dbus: for maemo plugin'
    'jack: for pcm_jack plugin'
    'libavtp: for pcm_aaf plugin'
    'libsamplerate: for rate_samplerate plugin'
    'libpulse: for conf_pulse, ctl_pulse and pcm_pulse plugins'
    'speexdsp: for pcm_speex and rate_speexrate plugins')

  cd "${srcdir:?}/alsa-plugins-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/alsa-plugins-${pkgver} directory! Package alsa-plugins Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  install -Dt "${pkgdir:?}/usr/share/doc/${pkgbase}" -m644 doc/README* doc/*.txt

  # remove maemo plugin configuration (it overrides defaults for pcm and ctl),
  # until a better way is found to package default overrides:
  # https://bugs.archlinux.org/task/65440
  rm -v "${pkgdir:?}/etc/alsa/conf.d/98-maemo.conf"

  # make a proper off-by-default config template out of the example file
  mv -v "${pkgdir:?}/etc/alsa/conf.d/99-pulseaudio-default.conf.example" \
    "${pkgdir:?}/usr/share/alsa/alsa.conf.d/99-pulseaudio-default.conf"

}

package_pulseaudio-alsa+clang() {

  pkgdesc="ALSA Configuration for PulseAudio"
  depends=("alsa-plugins+clang=${epoch}:${pkgver}" pulseaudio)
  provides=("pulseaudio-alsa" "pulseaudio-alsa=${epoch}:${pkgver}")
  conflicts=("pulseaudio-alsa")

  mkdir -p "${pkgdir:?}/etc/alsa/conf.d"
  ln -st "${pkgdir:?}/etc/alsa/conf.d" \
    /usr/share/alsa/alsa.conf.d/99-pulseaudio-default.conf

}
