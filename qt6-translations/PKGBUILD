pkgname=qt6-translations+clang
_qtver=6.2.4
pkgver=${_qtver/-/}
pkgrel=1
arch=(any)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='A cross-platform application and UI framework (Translations)'
provides=("qt6-translations" "qt6-translations=${pkgver}")
conflicts=("qt6-translations")
makedepends=(cmake ninja qt6-tools)
groups=(qt6)
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/qttranslations-everywhere-src-${_qtver}.tar.xz")
sha256sums=('bd1aac74a892c60b2f147b6d53bb5b55ab7a6409e63097d38198933f8024fa51')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  export PATH="/usr/lib/qt6/bin:${PATH}"
  cmake -B build -G Ninja -S "qttranslations-everywhere-src-${_qtver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build -j1

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

  install -d "${pkgdir:?}/usr/share/licenses"
  ln -s /usr/share/licenses/qt6-base "${pkgdir:?}/usr/share/licenses/qt6-translations"

}
