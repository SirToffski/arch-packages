pkgname=setools+clang
pkgver=4.4.0
pkgrel=2
pkgdesc="Policy analysis tools for SELinux"
groups=('selinux')
arch=('x86_64')
url="https://github.com/SELinuxProject/setools/wiki"
license=('GPL' 'LGPL')
depends=('libsepol>=3.2' 'libselinux>=3.2' 'python' 'python-networkx>=2.0' 'python-setuptools')
optdepends=(
  'python-pyqt5: needed for graphical tools'
  'qt5-tools: display apol help with Qt Assistant'
)
makedepends=('cython' 'python-tox')
checkdepends=('checkpolicy')
conflicts=("selinux-setools" "setools")
provides=("setools" "setools=${pkgver}" "selinux-setools=${pkgver}-${pkgrel}")
source=("https://github.com/SELinuxProject/setools/releases/download/${pkgver}/setools-${pkgver}.tar.bz2")
sha256sums=('f3786677e40b7f16a226f48f233dcf835e700739614a7dbed2ff61cc9607814e')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cd "${srcdir:?}/setools" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/setools directory! Build Failed! \E[0m"
    exit 1
  )

  python setup.py build_ext
  python setup.py build

}

check() {

  cd "${srcdir:?}/setools" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/setools directory! Check Failed! \E[0m"
    exit 1
  )

  python setup.py test

}

package() {

  cd "${srcdir:?}/setools" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/setools directory! Package Failed! \E[0m"
    exit 1
  )

  python setup.py install --root="${pkgdir:?}" --optimize=1 --skip-build

}
