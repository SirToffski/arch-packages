pkgname=libxinerama+clang
pkgver=1.1.4
pkgrel=4
pkgdesc="X11 Xinerama extension library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
provides=("libxinerama" "libxinerama=${pkgver}")
conflicts=("libxinerama")
depends=('libxext')
makedepends=('xorg-util-macros' 'xorgproto')
source=("${url}/releases/individual/lib/libXinerama-${pkgver}.tar.bz2")
sha512sums=('cff777ad942614fbf6bc6d8529f399e62debf3ecbf6cc0694a94e38c022bf929ffd5636fb59f55533c394d89b23af3ea51fa5128927f12a85787e16239330f14')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libXinerama-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxinerama-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr

  make

}

package() {

  cd "${srcdir:?}/libXinerama-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxinerama-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  install -m755 -d "${pkgdir:?}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
