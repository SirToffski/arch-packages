pkgname=gnutls+clang
pkgver=3.7.4
pkgrel=2
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=('x86_64')
license=('GPL3' 'LGPL2.1')
url="https://www.gnutls.org/"
options=('!zipman')
provides=('gnutls' "gnutls=${pkgver}")
conflicts=('gnutls')
depends=('gcc-libs' 'libtasn1' 'readline' 'zlib' 'nettle' 'p11-kit' 'libidn2'
  'libidn2.so' 'libunistring' 'brotli')
checkdepends=('net-tools')
optdepends=('guile: for use with Guile bindings')
source=("https://www.gnupg.org/ftp/gcrypt/gnutls/v3.7/gnutls-${pkgver}.tar.xz"{,.sig})
sha256sums=('e6adbebcfbc95867de01060d93c789938cf89cc1d1f6ef9ef661890f6217451f'
  'SKIP')
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871') # "Daiki Ueno <ueno@unixuser.org>"

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=0
  fi

}

build() {

  cd "${srcdir:?}/gnutls-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/gnutls-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --disable-static \
    --with-idn \
    --enable-openssl-compatibility \
    --enable-guile \
    --with-guile-site-dir=no \
    --with-default-trust-store-pkcs11="pkcs11:"

  make

}

check() {

  make check -C "gnutls-${pkgver}"

}

package() {

  make DESTDIR="${pkgdir:?}" install -C "gnutls-${pkgver}"

  # lots of .png files are put into infodir and are gzipped by makepkg! this needs to be fixed by using !zipman
  # gzip -9 all files in infodir and manpages manually
  find "${pkgdir:?}/usr/share/info" -name '*.info*' -exec gzip -n -9 {} \;
  find "${pkgdir:?}/usr/share/man" -exec gzip -n -9 {} \;

}
