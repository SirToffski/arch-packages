pkgbase=plasma-workspace-noname
pkgname=(plasma-workspace-noname plasma-wayland-session-noname)
pkgver=5.22.1
pkgrel=1
pkgdesc='KDE Plasma Workspace'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(knotifyconfig ksystemstats ktexteditor libqalculate kde-cli-tools appstream-qt
  xorg-xrdb xorg-xsetroot kactivitymanagerd kholidays xorg-xmessage milou prison kwin
  plasma-integration kpeople kactivities-stats libkscreen kquickcharts kuserfeedback kio-extras kio-fuse)
makedepends=(extra-cmake-modules kdoctools gpsd baloo networkmanager-qt plasma-wayland-protocols kunitconversion kinit)
groups=(plasma)
source=(
  "https://download.kde.org/stable/plasma/${pkgver}/plasma-workspace-${pkgver}.tar.xz"{,.sig}
  kde.pam
  lto.cmake
)
sha256sums=('61ad5adab412c935ae156c4de3f38d1e2640752c95100ad6996b9e51e8539725'
  'SKIP'
  '00090291204baabe9d6857d3b1419832376dd2e279087d718b64792691e86739'
  '7971e5dd106804e615bf270a1d660835ded44f5707263e40a0b402cd1d63348e')
validpgpkeys=(
  '2D1D5B0588357787DE9EE225EC94D18F7F05997E' # Jonathan Riddell <jr@jriddell.org>
  '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D' # Bhushan Shah <bshah@kde.org>
  'D07BD8662C56CB291B316EB2F5675605C74E02CF' # David Edmundson <davidedmundson@kde.org>
  '1FA881591C26B276D7A5518EEAAF29B42A678C20' # Marco Martin <notmart@gmail.com>
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    cmake -B build -S plasma-workspace-${pkgver} \
      -DCMAKE_TOOLCHAIN_FILE=../lto.cmake \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DBUILD_TESTING=OFF

  else

    cmake -B build -S plasma-workspace-${pkgver} \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DBUILD_TESTING=OFF

  fi

  cmake --build build

}

package_plasma-workspace-noname() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
    'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation'
    'kdepim-addons: displaying PIM events in the calendar'
    'appmenu-gtk-module: global menu support for GTK2 and some GTK3 applications'
    'baloo: Baloo search runner' 'discover: manage applications installation from the launcher')
  provides=(plasma-workspace)
  conflicts=(plasma-workspace)
  backup=('etc/pam.d/kde')

  DESTDIR="${pkgdir:?}" cmake --install build

  install -Dm644 "${srcdir:?}/kde.pam" "${pkgdir:?}/etc/pam.d/kde"

  # Split plasma-wayland scripts
  rm -r "${pkgdir:?}/usr/share/wayland-sessions"

}

package_plasma-wayland-session-noname() {

  pkgdesc='Plasma Wayland session'
  depends=(plasma-workspace-noname qt5-wayland kwayland-integration xorg-xwayland)
  provides=(plasma-wayland-session)
  conflicts=(plasma-wayland-session)

  install -Dm644 build/login-sessions/plasmawayland.desktop "${pkgdir:?}/usr/share/wayland-sessions/plasmawayland.desktop"

}
