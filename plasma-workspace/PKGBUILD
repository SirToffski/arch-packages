pkgbase=plasma-workspace+clang
pkgname=(plasma-workspace+clang plasma-wayland-session+clang)
pkgver=5.22.5
pkgrel=3
pkgdesc='KDE Plasma Workspace'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(knotifyconfig ksystemstats ktexteditor libqalculate kde-cli-tools appstream-qt
  xorg-xrdb xorg-xsetroot kactivitymanagerd kholidays xorg-xmessage milou prison kwin
  plasma-integration kpeople kactivities-stats libkscreen kquickcharts kuserfeedback kio-extras kio-fuse)
makedepends=(extra-cmake-modules kdoctools gpsd baloo networkmanager-qt plasma-wayland-protocols kunitconversion kinit)
groups=(plasma)
source=(
  "https://download.kde.org/stable/plasma/${pkgver}/plasma-workspace-${pkgver}.tar.xz"{,.sig}
  kde.pam
  https://invent.kde.org/plasma/plasma-workspace/-/commit/6379d1ec.patch
  https://invent.kde.org/plasma/plasma-workspace/-/commit/26f848d4.patch
  https://invent.kde.org/plasma/plasma-workspace/-/commit/fc32a7f8.patch
)
sha256sums=('f22e12dea303478d949f69b88fcf30787ef7f5d098be55595ec562aee4b13606'
  'SKIP'
  '00090291204baabe9d6857d3b1419832376dd2e279087d718b64792691e86739'
  '7436b6ff6c8f7e85aa37d8c2db4ba5937e1d200194f1e9ce88ee6815ea5e6fe0'
  '00db1439abb101eae4b24c00e430b17125f71c2b949d2bb76e352d90b6ad5923'
  'a4b85a7ef9be3dee447a97db0fe0c1799f3faf18952fa928d382f8a612de517b')
validpgpkeys=(
  '2D1D5B0588357787DE9EE225EC94D18F7F05997E' # Jonathan Riddell <jr@jriddell.org>
  '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D' # Bhushan Shah <bshah@kde.org>
  'D07BD8662C56CB291B316EB2F5675605C74E02CF' # David Edmundson <davidedmundson@kde.org>
  '1FA881591C26B276D7A5518EEAAF29B42A678C20' # Marco Martin <notmart@gmail.com>
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  patch -d plasma-workspace-${pkgver} -p1 <6379d1ec.patch # Fix power management inhibition
  patch -d plasma-workspace-${pkgver} -p1 <26f848d4.patch # Fix systemd detection D-Bus calls
  patch -d plasma-workspace-${pkgver} -p1 <fc32a7f8.patch # Fix build with gpsd 3.23.1

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S plasma-workspace-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF

  cmake --build build

}

package_plasma-workspace+clang() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
    'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation'
    'kdepim-addons: displaying PIM events in the calendar'
    'appmenu-gtk-module: global menu support for GTK2 and some GTK3 applications'
    'baloo: Baloo search runner' 'discover: manage applications installation from the launcher')
  provides=(plasma-workspace "plasma-workspace=${pkgver}")
  conflicts=(plasma-workspace)
  backup=('etc/pam.d/kde')

  DESTDIR="${pkgdir:?}" cmake --install build

  install -Dm644 "${srcdir:?}/kde.pam" "${pkgdir:?}/etc/pam.d/kde"

  # Split plasma-wayland scripts
  rm -r "${pkgdir:?}/usr/share/wayland-sessions"

}

package_plasma-wayland-session+clang() {

  pkgdesc='Plasma Wayland session'
  depends=(plasma-workspace+clang qt5-wayland kwayland-integration xorg-xwayland)
  provides=(plasma-wayland-session "plasma-wayland-session=${pkgver}")
  conflicts=(plasma-wayland-session)

  install -Dm644 build/login-sessions/plasmawayland.desktop "${pkgdir:?}/usr/share/wayland-sessions/plasmawayland.desktop"

}
