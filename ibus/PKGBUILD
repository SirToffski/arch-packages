pkgbase=ibus+clang
pkgname=(ibus+clang libibus+clang)
pkgver=1.5.26
pkgrel=2
pkgdesc="Next Generation Input Bus for Linux"
arch=('x86_64')
url="https://github.com/ibus/ibus/wiki"
license=('LGPL')
depends=('dconf' 'gtk3' 'hicolor-icon-theme' 'libnotify' 'python-dbus' 'python-gobject'
  'iso-codes' 'librsvg')
makedepends=('gobject-introspection' 'vala' 'intltool' 'gnome-common' 'gtk-doc' 'gtk4' 'gtk2'
  'qt5-base' 'unicode-cldr' 'unicode-character-database' 'unicode-emoji' 'git')
options=('!emptydirs')
_commit=6a70ab0338206bd1c7d01a4e1874ea0ee5b3a9d3 # tags/1.5.26^0
source=("git+https://github.com/ibus/ibus#commit=${_commit}")
sha512sums=('SKIP')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/ibus" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/ibus directory! Prepare Failed! \E[0m"
    exit 1
  )

  # https://github.com/ibus/ibus/issues/2387
  git cherry-pick -n 17648f0522910480b6c5dd4f5356ca1f6c160bf5

  NOCONFIGURE=1 ./autogen.sh
}

build() {

  cd "${srcdir:?}/ibus" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/ibus directory! Build Failed! \E[0m"
    exit 1
  )

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus \
    --sysconfdir=/etc \
    --enable-dconf \
    --enable-wayland \
    --enable-gtk-doc \
    --enable-gtk4 \
    --disable-memconf \
    --enable-ui \
    --disable-python2 \
    --with-python=python3 \
    --with-ucd-dir=/usr/share/unicode/

  make

}

package_ibus+clang() {

  depends+=("libibus=$pkgver")
  provides=('ibus' "ibus=${pkgver}")
  conflicts=('ibus')

  make -C "ibus" DESTDIR="${pkgdir:?}" install
  make -C "ibus/src" DESTDIR="${pkgdir:?}" uninstall
  make -C "ibus/bindings" DESTDIR="${pkgdir:?}" uninstall
  make -C "ibus" DESTDIR="${pkgdir:?}" uninstall-pkgconfigDATA

}

package_libibus+clang() {

  pkgdesc="IBus support library"
  depends=(libg{lib,object,io}-2.0.so)
  provides=('libibus-1.0.so' 'libibus' "libibus=${pkgver}")
  conflicts=('libibus')

  make -C "ibus/src" DESTDIR="${pkgdir:?}" install
  make -C "ibus/bindings" DESTDIR="${pkgdir:?}" install
  make -C "ibus" DESTDIR="${pkgdir:?}" install-pkgconfigDATA

}
