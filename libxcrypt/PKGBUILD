pkgbase=libxcrypt+clang
pkgname=(libxcrypt+clang libxcrypt-compat+clang)
pkgver=4.4.28
pkgrel=2
arch=('x86_64')
url='https://github.com/besser82/libxcrypt/'
license=('GPL')
depends=('glibc')
makedepends=('git')
install=libxcrypt.install
_commit=50cf2b6dd4fdf04309445f2eec8de7051d953abf          # git rev-parse v${pkgver}
validpgpkeys=('678CE3FEE430311596DB8C16F52E98007594C21D') # BjÃ¶rn 'besser82' Esser
source=("git+https://github.com/besser82/libxcrypt.git#commit=${_commit}?signed")
sha256sums=('SKIP')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/libxcrypt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxcrypt directory! Prepare Failed! \E[0m"
    exit 1
  )

  autoreconf -fi

}

build() {

  mkdir build-libxcrypt build-libxcrypt-compat

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -Wno-gnu-statement-expression"
    CXXFLAGS="${CXXFLAGS} -Wno-gnu-statement-expression"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cd "${srcdir:?}/build-libxcrypt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/build-libxcrypt directory! Build Failed! \E[0m"
    exit 1
  )

  "${srcdir:?}/libxcrypt/configure" \
    --prefix=/usr \
    --disable-static \
    --enable-hashes=strong,glibc \
    --enable-obsolete-api=no \
    --disable-failure-tokens \
    --disable-silent-rules \
    --disable-valgrind

  make

  cd "${srcdir:?}/build-libxcrypt-compat" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/build-libxcrypt-compat directory! Build Failed! \E[0m"
    exit 1
  )

  "${srcdir:?}/libxcrypt/configure" \
    --prefix=/usr \
    --disable-static \
    --enable-hashes=strong,glibc \
    --enable-obsolete-api=glibc \
    --disable-failure-tokens

  make

}

check() {

  cd "${srcdir:?}/build-libxcrypt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/build-libxcrypt directory! Check Failed! \E[0m"
    exit 1
  )

  make check

}

package_libxcrypt+clang() {

  pkgdesc='Modern library for one-way hashing of passwords'
  provides=("libxcrypt" "libxcrypt=${pkgver}" 'libcrypt.so')
  conflicts=("libxcrypt")

  cd "${srcdir:?}/build-libxcrypt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/build-libxcrypt directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

}

package_libxcrypt-compat+clang() {

  pkgdesc='Modern library for one-way hashing of passwords - legacy API functions'
  depends=('libxcrypt+clang')

  cd "${srcdir:?}/build-libxcrypt-compat" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/build-libxcrypt-compat directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  rm -rf "${pkgdir:?}/usr/"{include,lib/{lib*.so,pkgconfig},share}

}
