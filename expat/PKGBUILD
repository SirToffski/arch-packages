pkgname=expat+clang
pkgver=2.4.5
pkgrel=2
pkgdesc='An XML parser library'
arch=('x86_64')
url="https://libexpat.github.io/"
license=('MIT')
depends=('glibc')
makedepends=('cmake' 'docbook2x')
provides=(expat "expat=${pkgver}" 'libexpat.so')
conflicts=(expat)
source=(
  "https://github.com/libexpat/libexpat/releases/download/R_${pkgver//./_}/expat-${pkgver}.tar.bz2"{,.asc}
)
sha256sums=('fbb430f964c7a2db2626452b6769e6a8d5d23593a453ccbc21701b74deabedff'
  'SKIP')
validpgpkeys=(3176EF7DB2367F1FCA4F306B1F9B0E909AF37285) # Sebastian Pipping

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S expat-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDOCBOOK_TO_MAN="/usr/bin/db2x_docbook2man"

  cmake --build build

}

check() {

  make -C build test

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

  install -vDm 644 "expat-${pkgver}/COPYING" -t "${pkgdir:?}/usr/share/licenses/${pkgname}/"
  install -vDm 644 "expat-${pkgver}/"{AUTHORS,Changes,README.md} \
    -t "${pkgdir:?}/usr/share/doc/${pkgname}/"

}
