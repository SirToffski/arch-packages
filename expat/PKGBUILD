pkgname=expat+clang
pkgver=2.4.1
pkgrel=1
pkgdesc='An XML parser library'
arch=('x86_64')
url="https://libexpat.github.io/"
license=('MIT')
depends=('glibc')
makedepends=('cmake' 'xmlto')
provides=(expat "expat=${pkgver}" 'libexpat.so')
conflicts=(expat)
source=(
  "https://github.com/libexpat/libexpat/releases/download/R_${pkgver//./_}/expat-${pkgver}.tar.bz2"{,.asc}
)
sha512sums=('b24e19c3f91e0f3d1ad3b08a810b8874e272ddfc6d4fd8f79c14c13eddf410648343b9f69fba2ce165be46c143cecf8d66cf446b4930da42f92def555ecd3408'
  'SKIP')
b2sums=('47a39b63023cc1a4bdb56a5b2e6a51d9dfdeb69b8d4138e9f822a48ab07b01ceff00dcadbce688a4d069961e99746f692f616c5a62d69cec2bd5f74c6066c317'
  'SKIP')
validpgpkeys=(3176EF7DB2367F1FCA4F306B1F9B0E909AF37285) # Sebastian Pipping

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/expat-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/expat-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    DOCBOOK_TO_MAN="xmlto man --skip-validation" CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" \
      ./configure \
      --prefix=/usr

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" make

  else

    DOCBOOK_TO_MAN="xmlto man --skip-validation" ./configure \
      --prefix=/usr

    make

  fi

}

check() {

  cd "${srcdir:?}/expat-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/expat-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  make check

}

package() {

  cd "${srcdir:?}/expat-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/expat-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make VERBOSE=1 DESTDIR="${pkgdir:?}" install
  install -vDm 644 COPYING -t "${pkgdir:?}/usr/share/licenses/${pkgname}/"
  install -vDm 644 {AUTHORS,Changes,README.md} \
    -t "${pkgdir:?}/usr/share/doc/${pkgname}/"

}
