pkgname=swaybg+clang
pkgver=1.1
pkgrel=2
pkgdesc='Wallpaper tool for Wayland compositors'
arch=(x86_64)
url='https://github.com/swaywm/swaybg'
license=(MIT)
provides=('swaybg' "swaybg=${pkgver}")
conflicts=('swaybg')
makedepends=(
  meson
  scdoc
  wayland-protocols
)
depends=(
  'wayland'
  'cairo'
  'gdk-pixbuf2' # For images other than PNG
)
source=(
  "swaybg-${pkgver}.tar.gz::https://github.com/swaywm/swaybg/releases/download/v${pkgver}/swaybg-${pkgver}.tar.gz"
  "swaybg-${pkgver}.tar.gz.sig::https://github.com/swaywm/swaybg/releases/download/v${pkgver}/swaybg-${pkgver}.tar.gz.sig"
)
sha256sums=('958f425310514ae2aae064595c3e7efba2187b0d2947136c49188558fdb367b1'
  'SKIP')
validpgpkeys=(
  "9DDA3B9FA5D58DD5392C78E652CB6609B22DA89A" # Drew DeVault
  "34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48" # Simon Ser
)

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

build() {

  meson setup "swaybg-${pkgver}" build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true

  meson compile -C build

}

package() {

  DESTDIR="${pkgdir:?}" meson install -C build
  install -Dm644 "swaybg-${pkgver}/LICENSE" -t "${pkgdir:?}/usr/share/licenses/swaybg"
  install -Dm644 "swaybg-${pkgver}/README.md" -t "${pkgdir:?}/usr/share/doc/swaybg"

}
