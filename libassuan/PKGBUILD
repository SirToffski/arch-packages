pkgname=libassuan+clang
pkgver=2.5.5
pkgrel=1
pkgdesc='IPC library used by some GnuPG related software'
url="https://www.gnupg.org/related_software/libassuan/"
arch=('x86_64')
license=('GPL3')
depends=('glibc' 'libgpg-error')
provides=('libassuan.so' libassuan "libassuan=${pkgver}")
conflicts=(libassuan)
source=("https://gnupg.org/ftp/gcrypt/libassuan/libassuan-${pkgver}.tar.bz2"{,.sig})
sha512sums=('70117f77aa43bbbe0ed28da5ef23834c026780a74076a92ec775e30f851badb423e9a2cb9e8d142c94e4f6f8a794988c1b788fd4bd2271e562071adf0ab16403'
  'SKIP')
b2sums=('24952e97c757b97c387ab4c2c4bf7b040f2874e9326c129805c7f5326fa14d80e083b0842e336a635531a2c8d4a66d428c816bae6b175f1c4518add1ffa3554d'
  'SKIP')
validpgpkeys=('6DAA6E64A76D2840571B4902528897B826403ADA') # "Werner Koch (dist signing 2020)"

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libassuan-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libassuan-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr

  make

}

check() {

  make check -C "libassuan-${pkgver}"

}

package() {

  make DESTDIR="${pkgdir:?}" install -C "libassuan-${pkgver}"

  install -vDm 644 "libassuan-${pkgver}/"{AUTHORS,NEWS,README,ChangeLog} \
    -t "${pkgdir:?}/usr/share/doc/libassuan"

}
