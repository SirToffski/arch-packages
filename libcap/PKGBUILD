pkgname=libcap+clang
pkgver=2.53
pkgrel=1
pkgdesc='POSIX 1003.1e capabilities'
arch=('x86_64')
url="https://sites.google.com/site/fullycapable/"
license=('GPL2')
depends=('glibc' 'pam')
makedepends=('linux-api-headers')
provides=('libcap' "libcap=${pkgver}" 'libcap.so' 'libpsx.so')
conflicts=('libcap')
source=(
  "https://kernel.org/pub/linux/libs/security/linux-privs/libcap2/libcap-${pkgver}.tar."{xz,sign}
  libcap-2.53-makefile.patch
  libcap-2.53-progs_ldflags.patch
)
sha256sums=('9546b7e5358a67a6a4fb0f51715b628efdeff00ee0e629666847ed7de3c63571'
  'SKIP'
  '27ecb35658d3019ffb8c8c45ef18c987a761f98c4957febc5bfee954fedb8c7f'
  '53bca36ca7b2fc7214d976e92079c8692d66f99f22970b7efb21626bad30234e')

validpgpkeys=(38A644698C69787344E954CE29EE848AE2CCF3F4) # Andrew G. Morgan <morgan@kernel.org>

_llvm=0

prepare() {

  cd "${srcdir:?}/libcap-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libcap-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
    sed -i -e 's/\$(CROSS_COMPILE)gcc/clang/;s/\$(CROSS_COMPILE)ar/llvm-ar/;s/\$(CROSS_COMPILE)ranlib/llvm-ranlib/' Make.Rules
  fi

  # SBINDIR is hardcoded to sbin. set to bin
  # add CPPFLAGS
  patch -Np1 -i "../libcap-2.53-makefile.patch"
  # add back LDFLAGS, so that executables are built with them
  patch -Np1 -i "../libcap-2.53-progs_ldflags.patch"

}

build() {

  cd "${srcdir:?}/libcap-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libcap-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  make KERNEL_HEADERS='/usr/include' lib='lib' prefix='/usr' sbindir='bin'

}

check() {

  cd "${srcdir:?}/libcap-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libcap-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  make -k test

}

package() {

  cd "${srcdir:?}/libcap-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libcap-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" RAISE_SETFCAP='no' lib='lib' prefix='/usr' sbindir='bin' install
  # docs
  install -vDm 644 {CHANGELOG,README} -t "${pkgdir:?}/usr/share/doc/${pkgname}/"
  install -vDm 644 pam_cap/capability.conf \
    -t "${pkgdir:?}/usr/share/doc/${pkgname}/examples/"

}
