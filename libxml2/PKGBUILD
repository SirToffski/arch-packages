pkgname=libxml2+clang
pkgver=2.9.12
pkgrel=1
pkgdesc='XML parsing library, version 2'
url='http://www.xmlsoft.org/'
arch=(x86_64)
license=(MIT)
provides=("libxml2" "libxml2=${pkgver}")
conflicts=("libxml2")
depends=(zlib readline ncurses xz icu)
makedepends=(python2 python git)
_commit=b48e77cf4f6fa0792c5f4b639707a2b0675e461b # tags/v2.9.12^0

source=(
  "libxml2::git+https://gitlab.gnome.org/GNOME/libxml2.git#commit=${_commit}"
  "https://www.w3.org/XML/Test/xmlts20130923.tar.gz"
  "libxml2-multilib.patch"
  "libxml2-2.9.8-python3-unicode-errors.patch"
  "libxml2-2.9.12-fix-lxml-corrupted-tree.patch"
  "libxml2-2.9.12-fix-formatting-regression.patch"
)

sha256sums=('SKIP'
  '9b61db9f5dbffa545f4b8d78422167083a8568c59bd1129f94138f936cf6fc1f'
  'ecebc33b731b24c88a5411574f92b813164bdb90754244be193d95e73e706776'
  '37eb81a8ec6929eed1514e891bff2dd05b450bcf0c712153880c485b7366c17c'
  '5b64aa7f2411eb7650491bf42e4a3531fc3158257b4745956975b9b9bc605f7d'
  'a0bb996bc73a13f3c3758f74d0a4198757e703710c5e3e462fd7a8e58a561563')

_llvm=0

pkgver() {

  cd "${srcdir:?}/libxml2" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxml2 directory! PkgVer Failed! \E[0m"
    exit 1
  )

  git describe --tags | sed 's/-rc/rc/;s/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  mkdir build-py{2,3}

  cd "${srcdir:?}/libxml2" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxml2 directory! Prepare Failed! \E[0m"
    exit 1
  )

  patch -Np1 <"../libxml2-multilib.patch"
  patch -Np1 <"../libxml2-2.9.8-python3-unicode-errors.patch"
  patch -Np1 <"../libxml2-2.9.12-fix-lxml-corrupted-tree.patch"
  patch -Np1 <"../libxml2-2.9.12-fix-formatting-regression.patch"

  sed -e '/cd fuzz; /d' -e 's/fuzz //g' -i Makefile.am
  autoreconf -fiv

}

_build() (

  cd "build-py${1:?}" || (
    echo -e "\E[1;31mCan't cd to build-py${1} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" \
      ../libxml2/configure \
      --prefix=/usr \
      --with-threads \
      --with-history \
      --with-python="/usr/bin/python${1:?}" \
      --with-icu

    PYTHONHASHSEED=0 CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" make

  else

    ../libxml2/configure \
      --prefix=/usr \
      --with-threads \
      --with-history \
      --with-python="/usr/bin/python${1:?}" \
      --with-icu

    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
    PYTHONHASHSEED=0 make

  fi

  find doc -type f -exec chmod 0644 {} +

)

build() {

  _build 2
  _build 3

}

check() {

  cd build-py3 || (
    echo -e "\E[1;31mCan't cd to build-py3 directory! Check Failed! \E[0m"
    exit 1
  )

  ln -s ../xmlconf .

  make check

}

package() {

  make -C build-py2 DESTDIR="${pkgdir:?}" install
  make -C build-py3/python DESTDIR="${pkgdir:?}" install
  install -Dm 644 build-py2/COPYING -t "${pkgdir:?}/usr/share/licenses/${pkgname}"

}
