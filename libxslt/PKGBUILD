pkgname=libxslt+clang
pkgver=1.1.34
pkgrel=5
pkgdesc="XML stylesheet transformation library"
url="http://xmlsoft.org/XSLT/"
arch=(x86_64)
license=(custom)
provides=('libxslt' "libxslt=${pkgver}")
conflicts=('libxslt')
depends=(libxml2 libgcrypt)
makedepends=(git docbook-xsl)
checkdepends=(docbook-xml python)
_commit=3653123f992db24cec417d12600f4c67388025e3 # tags/v1.1.34^0
source=("git+https://gitlab.gnome.org/GNOME/libxslt.git#commit=${_commit}"
  0001-Make-generate-id-deterministic.patch
  0002-Fix-manpage.patch)
sha256sums=('SKIP'
  '80ffc45016b2c0b8edeb332ba587a8523ea5ed856229e7f531edd0af9742bab3'
  'd7dea109c91958e2231c06d9b841853855a666f93b001a1999beab42c3345203')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/libxslt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxslt directory! Prepare Failed! \E[0m"
    exit 1
  )

  # Fix build with libxml libxml2-2.9.12
  git cherry-pick -n 9ae2f94df1721e002941b40665efb762aefcea1a
  git cherry-pick -n 824657768aea2cce9c23e72ba8085cb5e44350c7
  git cherry-pick -n 77c26bad0433541f486b1e7ced44ca9979376908
  # Make xsltproc output reproducible for packages which use it to generate documentation
  patch -Np1 -i ../0001-Make-generate-id-deterministic.patch

  # Fix manpage whitespace and rebuild the manpage
  patch -Np1 -i ../0002-Fix-manpage.patch
  rm doc/xsltproc.1

  NOCONFIGURE=1 ./autogen.sh

}

pkgver() {

  cd "${srcdir:?}/libxslt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxslt directory! PkgVer Failed! \E[0m"
    exit 1
  )

  git describe --tags | sed 's/^v//;s/-/+/g'

}

build() {

  cd "${srcdir:?}/libxslt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxslt directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"

  fi

  ./configure \
    --prefix=/usr \
    --without-python \
    --disable-static

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make

}

check() {

  cd "${srcdir:?}/libxslt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxslt directory! Check Failed! \E[0m"
    exit 1
  )

  make check

}

package() {

  cd "${srcdir:?}/libxslt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxslt directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  install -Dm644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/LICENSE"

}
