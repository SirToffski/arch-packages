pkgname=lxqt-build-tools+clang
pkgver=0.11.0
pkgrel=1
pkgdesc="Various packaging tools and scripts for LXQt applications"
arch=('x86_64')
url="https://github.com/lxqt/lxqt-build-tools"
license=("LGPL2.1")
provides=(lxqt-build-tools "lxqt-build-tools=${pkgver}")
conflicts=(lxqt-build-tools)
depends=("cmake" "qt5-tools" "git" "perl")
source=(
  "https://github.com/lxqt/lxqt-build-tools/releases/download/${pkgver}/lxqt-build-tools-${pkgver}.tar.xz"{,.asc}
)
sha256sums=('dd5d49e59f9b0552cb40118eb28a463f630b5de6e626b2910410b54ef3bcc1b9'
  'SKIP')
validpgpkeys=(
  "169704C6FB490C6892C7F23C37E0AF1FDA48F373" # Jerome Leclanche <jerome@leclan.ch>
  "7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3" # Alf Gaida <agaida@siduction.org>
  "19DFDF3A579BD509DBB572D8BE793007AD22DF7E" # Pedram Pourang <tsujan2000@gmail.com>
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S "lxqt-build-tools-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
