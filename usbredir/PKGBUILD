pkgname=usbredir+clang
pkgver=0.12.0
pkgrel=1
pkgdesc="USB traffic redirection protocol"
arch=('x86_64')
url="https://www.spice-space.org/usbredir.html"
license=('GPL2' 'LGPL2.1')
provides=('usbredir' "usbredir=${pkgver}")
conflicts=('usbredir')
depends=('libusb' 'glib2')
makedepends=('meson')
source=("https://spice-space.org/download/usbredir/usbredir-${pkgver}.tar.xz")
sha256sums=('fbb44025bf55e1ce8d84afc7596bfa47c8a36cd603c6fa440f9102c1c9761e6d')

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

build() {

  meson setup "usbredir-${pkgver}" build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true

  meson compile -C build

}

check() {

  meson test -C build --print-errorlogs

}

package() {

  meson install -C build --destdir="${pkgdir:?}"

}
