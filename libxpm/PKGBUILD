pkgname=libxpm+clang
pkgver=3.5.13
pkgrel=2
pkgdesc="X11 pixmap library"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
provides=(libxpm)
conflicts=(libxpm)
depends=('libxt' 'libxext')
makedepends=('xorg-util-macros')
source=("${url}/releases/individual/lib/libXpm-${pkgver}.tar.bz2"{,.sig})
sha512sums=('3b8f6e926272f39b1d95135c2274c00f5aaee1f9fb2ef79f71628df5edeb7ba20158819ef6a778101cc4092493a3b5b613c53634fdccadcc0fc85f0605e5e9a5'
  'SKIP')
validpgpkeys=(
  'C41C985FDCF1E5364576638B687393EE37D128F8' # Matthieu Herrb <matthieu.herrb@laas.fr>
  '3C2C43D9447D5938EF4551EBE23B7E70B467F0BF' # "Peter Hutterer (Who-T) <office@who-t.net>"
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libXpm-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libXpm-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" \
      ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --disable-static

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" make

  else

    ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --disable-static

    make

  fi

}

check() {

  cd "${srcdir:?}/libXpm-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libXpm-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  make check

}

package() {

  cd "${srcdir:?}/libXpm-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libXpm-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  install -m755 -d "${pkgdir:?}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
