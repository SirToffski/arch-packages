pkgname=ncurses+clang
pkgver=6.2
pkgrel=2
pkgdesc='System V Release 4.0 curses emulation library'
arch=(x86_64)
url='https://invisible-island.net/ncurses/ncurses.html'
license=(MIT)
provides=('ncurses' "ncurses=${pkgver}" libncurses++w.so libformw.so libmenuw.so libpanelw.so libncursesw.so)
conflicts=('ncurses')
depends=(glibc gcc-libs)
optdepends=('bash: for ncursesw6-config')
replaces=(alacritty-terminfo)
source=("https://ftp.gnu.org/pub/gnu/ncurses/ncurses-${pkgver}.tar.gz"{,.sig})
sha256sums=('30306e0c76e0f9f1f0de987cf1c82a5c21e1ce6568b9227f7da5b71cbea86c9d'
            'SKIP')
# NOTE: contacted upstream signing key holder about unsafe key (2021-05-02)
validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB') # Thomas Dickey <dickey@invisible-island.net>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/ncurses-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/ncurses-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  #autoreconf -vfi

}

build() {

  cd "${srcdir:?}/ncurses-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/ncurses-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -flto=thin"

  fi

  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --with-pkg-config-libdir=/usr/lib/pkgconfig \
    --with-shared \
    --with-normal \
    --without-debug \
    --without-ada \
    --enable-widec \
    --enable-pc-files \
    --with-cxx-binding \
    --with-cxx-shared \
    --with-manpage-format=normal

  make

}

package() {

  cd "${srcdir:?}/ncurses-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/ncurses-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  install -vDm 644 COPYING -t "${pkgdir:?}/usr/share/licenses/$pkgname/"

  # fool packages looking to link to non-wide-character ncurses libraries
  for lib in ncurses ncurses++ form panel menu; do
    echo "INPUT(-l${lib}w)" >"${pkgdir:?}/usr/lib/lib${lib}.so"
    ln -sv ${lib}w.pc "${pkgdir:?}/usr/lib/pkgconfig/${lib}.pc"
  done

  for lib in tic tinfo; do
    echo "INPUT(libncursesw.so.${pkgver:0:1})" >"${pkgdir:?}/usr/lib/lib${lib}.so"
    ln -sv libncursesw.so.${pkgver:0:1} "${pkgdir:?}/usr/lib/lib${lib}.so.${pkgver:0:1}"
    ln -sv ncursesw.pc "${pkgdir:?}/usr/lib/pkgconfig/${lib}.pc"
  done

  # some packages look for -lcurses during build
  echo 'INPUT(-lncursesw)' >"${pkgdir:?}/usr/lib/libcursesw.so"
  ln -s libncurses.so "${pkgdir:?}/usr/lib/libcurses.so"

}
