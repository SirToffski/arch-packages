pkgname=libedit+clang
_pkgver=20210910-3.1
pkgver=${_pkgver/-/_}
pkgrel=1
pkgdesc='Command line editor library providing generic line editing, history, and tokenization functions'
url='https://thrysoee.dk/editline/'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'ncurses' 'libncursesw.so')
provides=("libedit" "libedit=${pkgver}" 'libedit.so')
conflicts=("libedit")
source=("${url}/libedit-${_pkgver}.tar.gz")
sha256sums=('6792a6a992050762edcca28ff3318cdb7de37dccf7bc30db59fcd7017eed13c5')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi
}

build() {

  cd "${srcdir:?}/libedit-${_pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libedit-${_pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr

  make V=1

}

package() {

  cd "${srcdir:?}/libedit-${_pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libedit-${_pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  rm "${pkgdir:?}"/usr/share/man/man3/history.3 # conflicts with readline
  install -Dm 644 "${pkgdir:?}"/usr/share/man/man3/editline.3 "${pkgdir:?}"/usr/share/man/man3/el.3

  install -Dm 644 ChangeLog -t "${pkgdir:?}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir:?}/usr/share/licenses/${pkgname}"

}
