pkgbase=freetype2+clang
pkgname=(freetype2+clang freetype2-docs+clang)
pkgver=2.10.4
pkgrel=1
pkgdesc="Font rasterization library"
url="https://www.freetype.org/"
arch=(x86_64)
license=(GPL)
# adding harfbuzz for improved OpenType features auto-hinting
# introduces a cycle dep to harfbuzz depending on freetype wanted by upstream
depends=(zlib bzip2 sh libpng harfbuzz)
makedepends=(libx11 meson)
source=("https://download.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.xz"{,.sig}
    "https://download.savannah.gnu.org/releases/freetype/freetype-doc-${pkgver}.tar.xz"{,.sig}
    '0001-Enable-table-validation-modules.patch'
    '0002-Enable-subpixel-rendering.patch'
    '0003-Enable-infinality-subpixel-hinting.patch'
    '0004-Enable-long-PCF-family-names.patch'
    'freetype2.sh')
sha256sums=('86a854d8905b19698bbc8f23b860bc104246ce4854dcea8e3b0fb21284f75784'
    'SKIP'
    'cca1c19d1efa911bb685d919b5b0fe1279b0699bf8eb6a3d3bf9f02784758212'
    'SKIP'
    'f41df4f336d5e82e58733c7a4594476c9216cfc85c096327745a7e1b559e17e1'
    'dc77c1cfee4bf8e7e0690628c95d211df09e0d0750e4c8f075b78b5f105514f7'
    '21a62bc12b848320c686d602d8d4e3bcd51294a9def4dc9c301736e077b59f3f'
    '266384222f87a02fb02b2179828f6c26fe6d7b1fd09d1f7e3734e7fcb09cda2e'
    'f7f8e09c44f7552c883846e9a6a1efc50377c4932234e74adc4a8ff750606467')
validpgpkeys=(58E0C111E39F5408C5D3EC76C1A60EACE707FDA5) # Werner Lemberg <wl@gnu.org>

_extra_lto_flag=""

prepare() {

    if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
        _extra_lto_flag='-D b_lto_mode=thin'
    fi

    cd "${srcdir:?}/freetype-${pkgver}" || (
        echo -e "\E[1;31mCan't cd to ${srcdir:?}/freetype-${pkgver}! Prepare Failed! \E[0m"
        exit 1
    )

    patch -Np1 -i ../0001-Enable-table-validation-modules.patch
    patch -Np1 -i ../0002-Enable-subpixel-rendering.patch
    patch -Np1 -i ../0003-Enable-infinality-subpixel-hinting.patch
    patch -Np1 -i ../0004-Enable-long-PCF-family-names.patch

}

build() {

    meson setup "freetype-${pkgver}" build \
        --prefix /usr \
        --libexecdir lib \
        --sbindir bin \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D default_library=shared \
        -D debug=false \
        -D optimization=2 \
        -D b_ndebug=true \
        -D b_lto=true ${_extra_lto_flag} \
        -D b_pie=true

    ninja -C build

}

check() {

    meson test -C build

}

package_freetype2+clang() {

    provides=(freetype2 "freetype2=${pkgver}" libfreetype.so)
    conflicts=(freetype2)
    install=freetype2.install
    backup=(etc/profile.d/freetype2.sh)

    DESTDIR="${pkgdir:?}" ninja -C build install
    sed -i 's/Version:\s6\./Version: 23./' "${pkgdir:?}/usr/lib/pkgconfig/freetype2.pc"
    install -Dt "${pkgdir:?}/etc/profile.d" -m644 freetype2.sh

}

package_freetype2-docs+clang() {

    pkgdesc="Freetype documentation"
    depends=(freetype2+clang)
    provides=(freetype2-docs "freetype2-docs=${pkgver}")
    conflicts=(freetype2-docs)

    cd "${srcdir:?}/freetype-${pkgver}" || (
        echo -e "\E[1;31mCan't cd to ${srcdir:?}/freetype-${pkgver}! Package Failed! \E[0m"
        exit 1
    )

    install -d "${pkgdir:?}/usr/share/doc"
    cp -a docs "${pkgdir:?}/usr/share/doc/freetype2"

}
