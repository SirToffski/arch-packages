pkgbase=audit+clang
pkgname=('audit+clang' 'python-audit+clang')
pkgver=3.0.8
pkgrel=1
pkgdesc='Userspace components of the audit framework'
url='https://people.redhat.com/sgrubb/audit'
arch=('x86_64')
makedepends=('glibc' 'krb5' 'libcap-ng' 'libldap' 'swig' 'linux-headers' 'python')
license=('GPL')
options=('emptydirs')
source=(
  "https://people.redhat.com/sgrubb/audit/audit-${pkgver}.tar.gz"
  audit-3.0.8-config_paths.patch
  audit-3.0.8-flex-array-workaround.patch
  audit-3.0.8-undo-flex-array.patch
)
sha256sums=('b5f4d9b9ad69381ee18f33d3d918326aa52861509c901143f8a8c4ed5caa8913'
  'c3647e09d1a973595d8e43e373b8644dfb7131328c7fb3e229593af02c72ca42'
  'b5e2a01b87fcb3634b67b5899a66283fe2a5fb83a3ad324639c5cfc02fa26170'
  '6793c3358821b0d9c00f28289e47761caf5ba40a5016058bf35e876188d09084')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/audit-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/audit-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  cp /usr/include/linux/audit.h lib/

  patch -Np1 -i "${srcdir:?}/audit-3.0.8-flex-array-workaround.patch"

  autoreconf -fiv
  # replace the use of /sbin with /usr/bin in configs
  patch -Np1 -i "${srcdir:?}/audit-3.0.8-config_paths.patch"

}

build() {

  cd "${srcdir:?}/audit-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/audit-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/audit \
    --enable-gssapi-krb5=yes \
    --enable-systemd=yes \
    --with-libcap-ng=yes

  make

  [ -n "${SOURCE_DATE_EPOCH}" ] && touch -h -d @$SOURCE_DATE_EPOCH bindings/swig/python/audit.py

}

package_audit+clang() {

  depends=('glibc' 'krb5' 'libcap-ng')
  provides=('audit' "audit=${pkgver}" 'libaudit.so' 'libauparse.so')
  conflicts=('audit')
  backup=(
    etc/libaudit.conf
    etc/audit/audit-stop.rules
    etc/audit/auditd.conf
    etc/audit/audisp-remote.conf
    etc/audit/zos-remote.conf
    etc/audit/plugins.d/af_unix.conf
    etc/audit/plugins.d/au-remote.conf
    etc/audit/plugins.d/audispd-zos-remote.conf
    etc/audit/plugins.d/syslog.conf
  )

  make DESTDIR="${pkgdir:?}" install -C "audit-${pkgver}"

  cd "${pkgdir:?}" || (
    echo -e "\E[1;31mCan't cd to ${pkgdir:?} directory! Package audit Failed! \E[0m"
    exit 1
  )

  patch -Np1 -i "${srcdir:?}/audit-3.0.8-undo-flex-array.patch"

  # add log dir
  install -vdm 700 "${pkgdir:?}/var/log/audit"

  # remove legacy files
  rm -frv "${pkgdir:?}/usr/lib/audit"

  # remove files provided by python-audit
  rm -frv "${pkgdir:?}/usr/lib/python"*

}

package_python-audit+clang() {

  depends=('python' 'audit+clang')
  pkgdesc+=' (python bindings)'
  provides=('python-audit' "python-audit=${pkgver}")
  conflicts=('python-audit')

  make DESTDIR="${pkgdir:?}" install -C "audit-${pkgver}/bindings"

}
