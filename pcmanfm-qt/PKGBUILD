pkgname=pcmanfm-qt+clang
pkgver=1.1.0
pkgrel=1
pkgdesc="The LXQt file manager, Qt port of PCManFM"
arch=("x86_64")
groups=("lxqt")
url="https://github.com/lxqt/pcmanfm-qt"
license=("GPL2")
provides=("pcmanfm-qt" "pcmanfm-qt=${pkgver}")
conflicts=("pcmanfm-qt")
depends=("desktop-file-utils" "libfm-qt" "lxmenu-data" "libfm-qt.so")
makedepends=("lxqt-build-tools")
optdepends=(
  "gvfs: for various features like mounting without root, remote filesystems, encrypted filesystems and many others mentioned on https://github.com/lxqt/pcmanfm-qt/wiki"
  "gvfs-mtp: MTP support (Media Transfer Protocol)"
  "lxqt-archiver: archive management"
  "qt5-translations: allow keyboard shortcuts to function in some languages (e.g. German)"
)
source=(
  "https://github.com/lxqt/pcmanfm-qt/releases/download/${pkgver}/pcmanfm-qt-${pkgver}.tar.xz"{,.asc}
)
sha256sums=('09e62a70aabd4eb4c1942b0dc4a24051d09510de0a5e8370dd06e9358b14945f'
  'SKIP')
validpgpkeys=(
  "169704C6FB490C6892C7F23C37E0AF1FDA48F373" # Jerome Leclanche <jerome@leclan.ch>
  "7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3" # Alf Gaida <agaida@siduction.org>
  "19DFDF3A579BD509DBB572D8BE793007AD22DF7E" # Pedram Pourang <tsujan2000@gmail.com>
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S "pcmanfm-qt-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
