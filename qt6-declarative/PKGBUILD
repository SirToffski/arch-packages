pkgname=qt6-declarative+clang
_qtver=6.3.0
pkgver=${_qtver/-/}
pkgrel=2
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for QML and JavaScript languages'
depends=(qt6-base)
makedepends=(cmake python at-spi2-core qt6-shadertools)
conflicts=(qt6-quickcontrols2 qt6-declarative)
provides=(qt6-quickcontrols2 "qt6-declarative" "qt6-declarative=${pkgver}")
replaces=(qt6-quickcontrols2)
groups=(qt6)
source=(
  "https://ftp.fau.de/qtproject/archive/qt/${pkgver%.*}/${_qtver}/submodules/qtdeclarative-everywhere-src-${_qtver}.tar.xz"
  fix-qmlls-build.patch::https://code.qt.io/cgit/qt/qtdeclarative.git/patch/?id=95665d84
)
sha256sums=('d294b029dc2b2d4f65da516fdc3b8088d32643eb7ff77db135a8b9ce904caa37'
  '4efef4778b5ac0380c50e5980396d432fe46b4b2f563eed56e2d60b07e88a014')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  patch -d "qtdeclarative-everywhere-src-${_qtver}" -p1 <fix-qmlls-build.patch

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S "qtdeclarative-everywhere-src-${_qtver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_PUBLICBINDIR=usr/bin

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

  install -d "${pkgdir:?}/usr/bin"
  while IFS=$'\n' read -r _line; do
    ln -s "${_line%% *}" "${pkgdir:?}/${_line#* }"
  done <"${srcdir:?}/build/user_facing_tool_links.txt"

  install -d "${pkgdir:?}/usr/share/licenses"
  ln -s /usr/share/licenses/qt6-base "${pkgdir:?}/usr/share/licenses/qt6-declarative"

}
