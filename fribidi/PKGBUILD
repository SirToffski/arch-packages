pkgname=fribidi+clang
pkgver=1.0.12
pkgrel=1
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm"
url="https://github.com/fribidi/fribidi/"
arch=(x86_64)
license=(LGPL)
depends=(glibc)
makedepends=(git meson)
provides=(libfribidi.so fribidi "fribidi=${pkgver}")
conflicts=(fribidi)
_commit=6428d8469e536bcbb6e12c7b79ba6659371c435a # tags/v1.0.12^0
source=("git+https://github.com/fribidi/fribidi#commit=${_commit:?}")
sha256sums=('SKIP')

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

build() {

  meson setup "fribidi" build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D docs=false

  meson compile -C build

}

check() {

  meson test -C build --print-errorlogs

}

package() {

  meson install -C build --destdir "${pkgdir:?}"

}
