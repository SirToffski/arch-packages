pkgname=keyutils+clang
_tag='5678a1aae8834b5c16b5ed7dc72ef8836a29e122' # git rev-parse v${pkgver}
pkgver=1.6.3
pkgrel=1
pkgdesc='Linux Key Management Utilities'
arch=('x86_64')
url='https://www.kernel.org/'
license=('GPL2' 'LGPL2.1')
depends=('glibc' 'sh')
makedepends=('git')
provides=("keyutils" "keyutils=${pkgver}" 'libkeyutils.so')
conflicts=("keyutils")
backup=('etc/request-key.conf')
validpgpkeys=('A86E54B0D5E1B4E0AB7C640FFBB7576BA7CB0B6B') # David Howells <dhowells@redhat.com>
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/dhowells/keyutils.git#tag=${_tag}?signed"
  'request-key.conf.patch' 'reproducible.patch')
sha256sums=('SKIP'
  '203c602c61ed94ccd423a0a453d74143d678c641a9a4486367576ee8af2cb8d6'
  '7bb7400b2b8c8f0288c86ec9191f8964a1e682745a204013d5fc7c2e1a253d8e')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/keyutils" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/keyutils directory! Prepare Failed! \E[0m"
    exit 1
  )

  # fix paths of binaries in /etc/request-key.conf
  patch -Np0 -i ../request-key.conf.patch

  # make keyutils reproducible
  patch -Np1 -i ../reproducible.patch

}

build() {

  cd "${srcdir:?}/keyutils" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/keyutils directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  make CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}" SBINDIR='/usr/bin' BINDIR='/usr/bin'

}

package() {

  cd "${srcdir:?}/keyutils" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/keyutils directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" SBINDIR='/usr/bin' BINDIR='/usr/bin' LIBDIR='/usr/lib' USRLIBDIR='/usr/lib' install

}
