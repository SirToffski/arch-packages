pkgname=shaderc+clang
pkgver=2021.1
pkgrel=1
pkgdesc='Collection of tools, libraries and tests for shader compilation'
url='https://github.com/google/shaderc'
arch=('x86_64')
license=('Apache')
depends=('glibc' 'gcc-libs' 'glslang' 'spirv-tools')
makedepends=('asciidoctor' 'cmake' 'ninja' 'python' 'spirv-headers')
provides=('shaderc' "shaderc=${pkgver}" 'libshaderc_shared.so')
conflicts=('shaderc')
source=(
  "shaderc-${pkgver}.tar.gz::https://github.com/google/shaderc/archive/v${pkgver}.tar.gz"
  'lto.cmake'
)
sha512sums=('b6668bc2dd54cc745184c6a0b9a8f7ba6579fd6b67691a57179d03300f3054776e70b54b82e54b16e04aa14f3b5b28385079d2533657191a69e5698af3cde444'
  '03b3d99735a3a95098dd8e69468ab95b0f039f6c61aa4dcc2e12cee60d1ececf6e7d5d15d3f0687f3b5c6dd4118195c8365353df028bdde31a6b499dd80148ed')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  # de-vendor libs and disable git versioning
  sed '/examples/d;/third_party/d' -i shaderc-${pkgver}/CMakeLists.txt
  sed '/build-version/d' -i shaderc-${pkgver}/glslc/CMakeLists.txt
  cat <<-EOF >shaderc-${pkgver}/glslc/src/build-version.inc
"${pkgver}\\n"
"$(pacman -Q spirv-tools | cut -d \  -f 2 | sed 's/-.*//')\\n"
"$(pacman -Q glslang | cut -d \  -f 2 | sed 's/-.*//')\\n"
EOF

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    cmake -B build -S shaderc-${pkgver} \
      -DCMAKE_TOOLCHAIN_FILE=../lto.cmake \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DSHADERC_SKIP_TESTS=ON \
      -Dglslang_SOURCE_DIR=/usr/include/glslang

    sed -ie 's/\/usr\/bin\/ar/\/usr\/bin\/llvm-ar/g' build/libshaderc/CMakeFiles/shaderc_combined_genfile.dir/build.make

  else

    cmake -B build -S shaderc-${pkgver} \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DSHADERC_SKIP_TESTS=ON \
      -Dglslang_SOURCE_DIR=/usr/include/glslang

  fi

  cmake --build build

  asciidoctor -b manpage "shaderc-${pkgver}/glslc/README.asciidoc" -o glslc.1

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build
  install -Dm 644 glslc.1 -t "${pkgdir:?}/usr/share/man/man1"

}
