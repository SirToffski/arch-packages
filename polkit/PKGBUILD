pkgname=polkit+clang
pkgver=0.119
pkgrel=1
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=(x86_64)
license=(LGPL)
url="https://www.freedesktop.org/wiki/Software/polkit/"
provides=('polkit' "polkit=${pkgver}")
conflicts=('polkit')
depends=(glib2 pam expat systemd js78)
makedepends=(intltool gtk-doc gobject-introspection git autoconf-archive)
backup=(etc/pam.d/polkit-1)
_commit=2e5348bf4eb0ef984db32f7f96ec6722d441c6ca
source=("git+https://gitlab.freedesktop.org/polkit/polkit.git#commit=${_commit}")
sha256sums=('SKIP')

_llvm=0

pkgver() {

  cd "${srcdir:?}/polkit" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/polkit directory! PkgVer Failed! \E[0m"
    exit 1
  )

  git describe --tags | sed 's/-/+/g'

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/polkit" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/polkit directory! Prepare Failed! \E[0m"
    exit 1
  )

  NOCONFIGURE=1 ./autogen.sh

}

build() {

  cd "${srcdir:?}/polkit" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/polkit directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -flto=thin"

  fi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --enable-libsystemd-login=yes \
    --disable-static \
    --enable-gtk-doc \
    --with-os-type=redhat

  make

}

check() {

  cd "${srcdir:?}/polkit" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/polkit directory! Check Failed! \E[0m"
    exit 1
  )

  make -k check || :

}

package() {

  cd "${srcdir:?}/polkit" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/polkit directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install \
    dbusconfdir=/usr/share/dbus-1/system.d \
    rulesdir=/usr/share/polkit-1/rules.d

  install -d -o root -g 102 -m 750 "${pkgdir:?}"/{etc,usr/share}/polkit-1/rules.d

  install -Dm644 /dev/stdin "${pkgdir:?}/usr/lib/sysusers.d/${pkgname}.conf" <<END
u polkitd 102 "PolicyKit daemon"
m polkitd proc
END

}
