pkgname=polkit+clang
pkgver=0.120
pkgrel=4
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=(x86_64)
license=(LGPL)
url="https://www.freedesktop.org/wiki/Software/polkit/"
provides=('polkit' "polkit=${pkgver}")
conflicts=('polkit')
depends=(glib2 pam expat systemd js78)
makedepends=(meson gtk-doc gobject-introspection git)
checkdepends=(python-dbusmock)
backup=(etc/pam.d/polkit-1)
_commit=92b910ce2273daf6a76038f6bd764fa6958d4e8e
source=(
  "git+https://gitlab.freedesktop.org/polkit/polkit.git#commit=${_commit}"
  meson-0.61.diff
)
sha256sums=('SKIP'
  '45d08bbb76e1e22ca8e698df04652b7aceaded34f9b76ce6b68152fb26b0de8d')

_llvm=0

pkgver() {

  cd "${srcdir:?}/polkit" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/polkit directory! PkgVer Failed! \E[0m"
    exit 1
  )

  git describe --tags | sed 's/-/+/g'

}

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

  cd "${srcdir:?}/polkit" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/polkit directory! Prepare Failed! \E[0m"
    exit 1
  )

  # CVE-2021-4034
  git cherry-pick -n a2bf5c9c83b6ae46cbd5c779d3055bff81ded683

  # Fix build with Meson 0.61.0
  git apply -3 "${srcdir:?}/meson-0.61.diff"

}

build() {

  meson setup polkit build \
    --prefix=/usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D session_tracking=libsystemd-login \
    -D os_type=redhat \
    -D tests=true \
    -D gtk_doc=true \
    -D man=true \
    -D examples=true

  meson compile -C build

}

check() {

  meson test -C build --print-errorlogs -t 3

}

package() {

  meson install -C build --destdir "${pkgdir:?}"

  install -d -o root -g 102 -m 750 "${pkgdir:?}"/{etc,usr/share}/polkit-1/rules.d
  mv "${pkgdir:?}"/{etc,usr/share}/polkit-1/rules.d/50-default.rules

  install -Dm644 /dev/stdin "${pkgdir:?}/usr/lib/sysusers.d/${pkgname}.conf" <<END
u polkitd 102 "PolicyKit daemon"
m polkitd proc
END

}
