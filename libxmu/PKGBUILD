pkgname=libxmu+clang
pkgver=1.1.3
pkgrel=2
pkgdesc="X11 miscellaneous micro-utility library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
provides=(libxmu "libxmu=${pkgver}")
conflicts=(libxmu)
depends=('libxext' 'libxt')
makedepends=('xorg-util-macros')
license=('custom')
source=("${url}/releases/individual/lib/libXmu-${pkgver}.tar.bz2")
sha512sums=('8c6cc65b22aa031ad870dd92736681a068a0878a425a53dbed909943da1136c4a24034d467cfd3785c3a8d78f66850b69f1ebe1eb24aaf9bc176b1d171a5c762')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libXmu-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}libXmu-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" \
      ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --disable-static

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" make

  else

    ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --disable-static

    make

  fi

}

package() {

  cd "${srcdir:?}/libXmu-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}libXmu-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  install -m755 -d "${pkgdir:?}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
