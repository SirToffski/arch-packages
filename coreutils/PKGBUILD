pkgname=coreutils+clang
pkgver=9.0
pkgrel=2
pkgdesc='The basic file, shell and text manipulation utilities of the GNU operating system'
arch=('x86_64')
license=('GPL3')
url='https://www.gnu.org/software/coreutils/'
provides=('coreutils' "coreutils=${pkgver}")
conflicts=('coreutils')
depends=('glibc' 'acl' 'attr' 'gmp' 'libcap' 'openssl' 'libselinux')
source=(
  "https://ftp.gnu.org/gnu/coreutils/coreutils-${pkgver}.tar.xz"{,.sig}
  '01-fix-fs72253.patch'
)
validpgpkeys=(
  '6C37DC12121A5006BC1DB804DF6FD971306037D9' # PÃ¡draig Brady
)
sha256sums=('ce30acdf4a41bc5bb30dd955e9eaa75fa216b4e3deb08889ed32433c7b3b97ce'
  'SKIP'
  'aefec296212c10f8ddae10225216847f537e573d80b678161f453b34fd183bf5')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/coreutils-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/coreutils-${pkgver} build directory! Prepare Failed! \E[0m"
    exit 1
  )

  patch -p1 -N -i "${srcdir:?}/01-fix-fs72253.patch"

  # tail -F fails to find out that files are removed, in test VM
  # so disable the tests which verify this
  sed '/^  tests\/tail-2\/assert\.sh\s/d' -i tests/local.mk
  sed '/^  tests\/tail-2\/inotify-dir-recreate\.sh\s/d' -i tests/local.mk

  # some tests create directories with long name, which does not work on GitHub Actions
  sed '/^  tests\/du\/long-from-unreadable\.sh\s/d' -i tests/local.mk
  sed '/^  tests\/rm\/deep-2\.sh\s/d' -i tests/local.mk

}

build() {

  cd "${srcdir:?}/coreutils-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/coreutils-${pkgver} build directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  aclocal -I m4
  autoconf -f
  autoheader -f
  automake -f

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --with-openssl \
    --with-selinux \
    --enable-no-install-program=groups,hostname,kill,uptime

  make

}

check() {

  make check -C coreutils-${pkgver}

}

package() {

  DESTDIR="${pkgdir:?}" make install -C coreutils-${pkgver}

}
