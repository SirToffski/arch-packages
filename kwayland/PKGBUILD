pkgname=kwayland+clang
pkgver=5.83.0
pkgrel=1
pkgdesc='Qt-style Client and Server library wrapper for the Wayland libraries'
arch=(x86_64)
url='https://www.kde.org'
license=(LGPL)
provides=("kwayland" "kwayland=${pkgver}")
conflicts=("kwayland")
depends=(qt5-wayland)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc wayland-protocols plasma-wayland-protocols)
groups=(kf5)
source=(
  "https://download.kde.org/stable/frameworks/${pkgver%.*}/kwayland-${pkgver}.tar.xz"{,.sig}
  'lto.cmake'
)
sha256sums=('8ccc71f363f93807ea8cbf7d07310a6e9f45a7085e9ee68d2f11d768453dfab9'
  'SKIP'
  '7971e5dd106804e615bf270a1d660835ded44f5707263e40a0b402cd1d63348e')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    cmake -B build -S kwayland-${pkgver} \
      -DCMAKE_TOOLCHAIN_FILE=../lto.cmake \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DBUILD_TESTING=OFF \
      -DBUILD_QCH=ON

  else

    cmake -B build -S kwayland-${pkgver} \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DBUILD_TESTING=OFF \
      -DBUILD_QCH=ON

  fi

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
