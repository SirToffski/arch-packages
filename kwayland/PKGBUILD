pkgname=kwayland+clang
pkgver=5.94.0
pkgrel=2
pkgdesc='Qt-style Client and Server library wrapper for the Wayland libraries'
arch=(x86_64)
url='https://www.kde.org'
license=(LGPL)
provides=("kwayland" "kwayland=${pkgver}")
conflicts=("kwayland")
depends=(qt5-wayland)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc wayland-protocols plasma-wayland-protocols)
groups=(kf5)
source=(
  "https://download.kde.org/stable/frameworks/${pkgver%.*}/kwayland-${pkgver}.tar.xz"{,.sig}
  fix-pri.patch
)
sha256sums=('b2a4d8e1b4d81ce798c991cfb34210ba095f6406a37f4714ae3ab64abaca2636'
  'SKIP'
  'ffb0188e44140a015b2a66a3d5ef230c1cada4cb640f80d9e0522855376d8e53')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  patch -d kwayland-${pkgver} -p1 <fix-pri.patch

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S kwayland-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
