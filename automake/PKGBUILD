pkgname=automake+clang
pkgver=1.16.5
pkgrel=1
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=('any')
license=('GPL')
provides=(automake "automake=${pkgver}")
conflicts=(automake)
url="https://www.gnu.org/software/automake"
groups=('base-devel')
depends=('perl' 'bash')
makedepends=('autoconf')
checkdepends=('dejagnu' 'gcc-fortran' 'java-environment' 'vala' 'emacs' 'cscope'
  'expect' 'ncompress' 'gettext' 'lzip' 'zip' 'sharutils' 'help2man'
  # disable TeX tests for now, lots of them fail and need upstream
  # fixes for current texlive release
  #'texlive-bin' 'texinfo'
  'python' 'python-virtualenv')
source=("https://ftp.gnu.org/gnu/automake/automake-${pkgver}.tar.xz"{,.sig})
sha256sums=('f01d58cd6d9d77fbdca9eb4bbd5ead1988228fdb73d6f7a201f5f8d6b118b469'
  'SKIP')
validpgpkeys=(
  'E1622F96D2BB4E58018EEF9860F906016E407573' # Stefano Lattarini
  'F2A38D7EEB2B66405761070D0ADEE10094604D37' # Mathieu Lirzin
  '155D3FC500C834486D1EEA677FD9FCCB000BEEEE' # Jim Meyering
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cd "${srcdir:?}/automake-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/automake-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  ./configure \
    --build="${CHOST:?}" \
    --prefix=/usr

  make

}

check() {

  make check -C "automake-${pkgver}"

}

package() {

  make DESTDIR="${pkgdir:?}" install -C "automake-${pkgver}"

}
