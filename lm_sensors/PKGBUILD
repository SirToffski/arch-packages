pkgname=lm_sensors+clang
pkgver=3.6.0.r41.g31d1f125
_commit=31d1f125d8076f1c8c8f3224b31d240e6e6a1763
#_pkgver=${pkgver//./-}
pkgrel=1
epoch=1
pkgdesc="Collection of user space tools for general SMBus access and hardware monitoring"
arch=('x86_64')
url="https://hwmon.wiki.kernel.org/lm_sensors"
license=('GPL' 'LGPL')
optdepends=('rrdtool: for logging with sensord'
  'perl: for sensor detection and configuration convert')
makedepends=('rrdtool' 'perl' 'git')
provides=('lm_sensors' "lm_sensors=${pkgver}" 'libsensors.so')
conflicts=('lm_sensors')
backup=('etc/sensors3.conf' 'etc/healthd.conf' 'etc/conf.d/sensord')
#source=(lm_sensors-${pkgver}.tar.gz::https://github.com/lm-sensors/lm-sensors/archive/V${_pkgver}.tar.gz
source=("lm_sensors::git+https://github.com/groeck/lm-sensors.git#commit=${_commit}"
  healthd healthd.conf healthd.service sensord.conf)
sha256sums=('SKIP'
  '0ac9afb2a9155dd74ab393756ed552cd542dde1081149beb2ab4ec7ff55b8f4a'
  '5d17a366b175cf9cb4bb0115c030d4b8d91231546f713784a74935b6e533da9f'
  '2638cd363e60f8d36bcac468f414a6ba29a1b5599f40fc651ca953858c8429d7'
  '23bebef4c250f8c0aaba2c75fd3d2c8ee9473cc91a342161a9f5b3a34ddfa9e5')
validpgpkeys=('7CA69F4460F1BDC41FD2C858A5526B9BB3CD4E6A')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/lm_sensors" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/lm_sensors directory! Prepare Failed! \E[0m"
    exit 1
  )

  sed -i 's|/etc/sysconfig|/etc/conf.d|' prog/{detect/sensors-detect,init/{sensord,lm_sensors}.service}
  sed -i 's/EnvironmentFile=/EnvironmentFile=-/' prog/init/lm_sensors.service

}

pkgver() {

  cd "${srcdir:?}/lm_sensors" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/lm_sensors directory! PkgVer Failed! \E[0m"
    exit 1
  )

  git describe --long --tags | sed 's/V//;s/\([^-]*-g\)/r\1/;s/-/./g'

}

build() {

  cd "${srcdir:?}/lm_sensors" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/lm_sensors directory! PkgVer Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    make PREFIX=/usr CC=clang AR=llvm-ar LD=lld

  else

    make PREFIX=/usr

  fi

}

package() {

  cd "${srcdir:?}/lm_sensors" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/lm_sensors directory! Package Failed! \E[0m"
    exit 1
  )

  make PROG_EXTRA=sensord BUILD_STATIC_LIB=0 \
    PREFIX=/usr SBINDIR=/usr/bin MANDIR=/usr/share/man DESTDIR="${pkgdir:?}" install

  install -D -m755 "${srcdir:?}/healthd" "${pkgdir:?}/usr/bin/healthd"

  install -D -m644 "${srcdir:?}/healthd.conf" "${pkgdir:?}/etc/healthd.conf"
  install -D -m644 "${srcdir:?}/sensord.conf" "${pkgdir:?}/etc/conf.d/sensord"

  install -D -m644 "${srcdir:?}/healthd.service" "${pkgdir:?}/usr/lib/systemd/system/healthd.service"
  install -D -m644 prog/init/*.service "${pkgdir:?}/usr/lib/systemd/system/"

}
