pkgname=libavif+clang
pkgver=0.9.3
pkgrel=1
pkgdesc="Library for encoding and decoding .avif files"
arch=(x86_64)
url="https://github.com/AOMediaCodec/libavif"
license=(BSD)
provides=("libavif" "libavif=${pkgver}")
conflicts=("libavif")
depends=(glibc aom dav1d rav1e svt-av1 zlib libpng libjpeg libyuv)
makedepends=(cmake nasm pkgconf gdk-pixbuf2)
source=(
  "https://github.com/AOMediaCodec/libavif/archive/v${pkgver}/libavif-${pkgver}.tar.gz"
  fix-compilation-with-clang-13.patch
)
sha256sums=('bcd9a1f57f982a9615eb7e2faf87236dc88eb1d0c886f3471c7440ead605060d'
  '13f4355f1cfb997d1cb03990476d36171448d258cecea7b9f4bfbb28e21f036a')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  patch -d "libavif-${pkgver}" -p1 <fix-compilation-with-clang-13.patch

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S libavif-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DAVIF_BUILD_APPS=ON \
    -DAVIF_CODEC_AOM=ON \
    -DAVIF_CODEC_DAV1D=ON \
    -DAVIF_CODEC_RAV1E=ON \
    -DAVIF_CODEC_SVT=ON \
    -DAVIF_BUILD_GDK_PIXBUF=ON

  make -C build

}

package() {

  make -C build DESTDIR="${pkgdir:?}" install

  install -Dm644 "libavif-${pkgver}/LICENSE" -t "${pkgdir?}/usr/share/licenses/${pkgname}/"

}
