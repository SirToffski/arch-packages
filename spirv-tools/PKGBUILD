pkgname=spirv-tools+clang
pkgver=2022.1
pkgrel=1
pkgdesc="API and commands for processing SPIR-V modules"
arch=('x86_64')
url="https://www.khronos.org/vulkan/"
license=('custom')
provides=('spirv-tools' "spirv-tools=${pkgver}")
conflicts=('spirv-tools')
groups=(vulkan-devel)
depends=('gcc-libs')
makedepends=('cmake' 'python' 'git' 'spirv-headers')
source=("git+https://github.com/KhronosGroup/SPIRV-Tools.git#tag=v${pkgver}")
sha256sums=('SKIP')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake \
    -B build \
    -S SPIRV-Tools \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DSPIRV_WERROR=Off \
    -DBUILD_SHARED_LIBS=ON \
    -DSPIRV_TOOLS_BUILD_STATIC=OFF \
    -DSPIRV-Headers_SOURCE_DIR=/usr

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

  install -Dm644 SPIRV-Tools/LICENSE "${pkgdir:?}/usr/share/licenses/${pkgname}/LICENSE"

}
