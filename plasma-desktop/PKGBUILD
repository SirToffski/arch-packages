pkgname=plasma-desktop+clang
pkgver=5.22.3
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
provides=("plasma-desktop" "plasma-desktop=${pkgver}")
conflicts=("plasma-desktop")
source=(
  "https://download.kde.org/stable/plasma/${pkgver}/plasma-desktop-${pkgver}.tar.xz"{,.sig}
  'lto.cmake'
)
depends=(gawk kdelibs4support polkit-kde-agent libxkbfile kmenuedit systemsettings baloo accountsservice xdg-user-dirs)
optdepends=('plasma-nm: Network manager applet'
  'powerdevil: power management, suspend and hibernate support'
  'kscreen: screen management'
  'ibus: kimpanel IBUS support'
  'scim: kimpanel SCIM support'
  'kaccounts-integration: OpenDesktop integration plugin'
  'packagekit-qt5: to install new krunner plugins')
replaces=(user-manager knetattach)
makedepends=(extra-cmake-modules kdoctools xf86-input-evdev xf86-input-synaptics xf86-input-libinput xorg-server-devel
  scim kdesignerplugin kaccounts-integration intltool packagekit-qt5 kinit)
groups=(plasma)
sha256sums=('bd5b838572517859e7fda51f39ed599799e3b758b3edddc3865dd6b63a37d610'
            'SKIP'
            '7971e5dd106804e615bf270a1d660835ded44f5707263e40a0b402cd1d63348e')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E' # Jonathan Riddell <jr@jriddell.org>
  '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'             # Bhushan Shah <bshah@kde.org>
  'D07BD8662C56CB291B316EB2F5675605C74E02CF'             # David Edmundson <davidedmundson@kde.org>
  '1FA881591C26B276D7A5518EEAAF29B42A678C20'             # Marco Martin <notmart@gmail.com>
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    cmake -B build -S plasma-desktop-${pkgver} \
      -DCMAKE_TOOLCHAIN_FILE=../lto.cmake \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DBUILD_TESTING=OFF

  else

    cmake -B build -S plasma-desktop-${pkgver} \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DBUILD_TESTING=OFF

  fi

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
