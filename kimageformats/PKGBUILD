pkgname=kimageformats+clang
pkgver=5.83.0
pkgrel=4
pkgdesc='Image format plugins for Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
provides=("kimageformats" "kimageformats=${pkgver}")
conflicts=("kimageformats")
depends=(qt5-base)
makedepends=(extra-cmake-modules karchive libavif openexr libheif)
optdepends=('karchive: plugin for Krita and OpenRaster images'
  'libavif: AVIF format support'
  'openexr: EXR format support'
  'libheif: HEIF format support')
groups=(kf5)
source=(
  "https://download.kde.org/stable/frameworks/${pkgver%.*}/kimageformats-${pkgver}.tar.xz"{,.sig}
  'lto.cmake'
)
sha256sums=('dc3965efbf9f2a42b78bbaea3d825b0946a27ad5fc79395cd405c858f38d8917'
            'SKIP'
            '7971e5dd106804e615bf270a1d660835ded44f5707263e40a0b402cd1d63348e')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    cmake -B build -S kimageformats-${pkgver} \
      -DCMAKE_TOOLCHAIN_FILE=../lto.cmake \
      -DBUILD_TESTING=OFF \
      -DKIMAGEFORMATS_HEIF=ON

  else

    cmake -B build -S kimageformats-${pkgver} \
      -DBUILD_TESTING=OFF \
      -DKIMAGEFORMATS_HEIF=ON

  fi

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
