pkgname=libepoxy-noname
pkgver=1.5.8
pkgrel=1
pkgdesc="Library handling OpenGL function pointer management"
url="https://github.com/anholt/libepoxy"
arch=(x86_64)
license=(MIT)
provides=(libepoxy)
conflicts=(libepoxy)
depends=(glibc)
makedepends=(python mesa-libgl git meson doxygen graphviz)
_commit=2a1665c632d47c2c73f4e8e1d2f2db62b26682d2 # tags/1.5.8^0
source=("git+https://github.com/anholt/libepoxy#commit=$_commit")
sha256sums=('SKIP')

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

pkgver() {

  cd "${srcdir:?}/libepoxy" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libepoxy directory! PkgVer Failed! \E[0m"
    exit 1
  )

  git describe --tags | sed 's/-/+/g'

}

build() {

  meson setup libepoxy build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true

  meson compile -C build
}

check() {

  meson test -C build --print-errorlogs

}

package() {

  meson install -C build --destdir="${pkgdir:?}"
  install -Dm644 libepoxy/COPYING "${pkgdir:?}/usr/share/licenses/$pkgname/LICENSE"

}
