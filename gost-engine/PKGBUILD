# Maintainer: Grigory Vasilyev <echo "h0tc0d3(-*A*-)g-m*a-i-l(-d#t-)c#m" | sed -e 's/-//ig;s/*//ig;s/(A)/@/i;s/#/o/ig;s/(dot)/./i'>

validpgpkeys=('33ED753E14757D79FA17E57DC4C1F715B2B66B95')

pkgname=gost-engine
pkgver=1.1.1.r496.9b492b3
pkgrel=1
pkgdesc='Russian GOST R 34.10 and GOST R 34.11-2012 Crypto Algorithms for OpenSSL'
arch=('x86_64')
license=('Apache 2.0')
url='https://github.com/gost-engine/engine'
depends=('openssl')
makedepends=('cmake' 'git')
conflicts=('openssl-gost')
provides=('openssl-gost')

_commit=9b492b334213ea6dfb76d746e93c4b69a4b36175

source=(
  "gost-engine::git+https://github.com/gost-engine/engine.git#commit=${_commit}"
  'gost.cnf'
  "${pkgname:?}.install"
)

sha256sums=('SKIP'
            '04c3483d758e393d9831d2be4cee1eafd806eea15055438be7c7b4caeeccbfe6'
            '92a02f99c0e969e90f95bf43530359380134fcee54ccc52bf1f4c5607e4175a6')
sha512sums=('SKIP'
            '431f810f822a347135d10c99f98ed2516e6afe310def77fc9aa0b41dc0578b05e2684f77e1062ea934961720f6d11f73f27e38548bccfe61709e81f8d53c484e'
            'b352a4a127ae671a7175336d9ea47d8bad4338b25173e916cc07afdcb54d9da833302ea80eb965aec2828bbc99b65721e72c603feed0fefa70712ba0c0f5bb2c')

install=${pkgname:?}.install

_llvm=0

pkgver() {

  cd "${srcdir:?}/gost-engine" || (
    echo -e "\E[1m\E[31mCan't cd to ${srcdir}/gost-engine build directory! PkgVer Failed! \E[0m"
    exit 1
  )

  echo "1.1.1.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  openssl_version=$(openssl version | grep -ho "1\.[0-9]\.\w*")
  if ! echo "${openssl_version}" | grep -iq "1.1.1"; then
    echo -e "\E[1m\E[31mOpenSSL version ${openssl_version} is not compatible! \E[0m"
    exit 1
  fi

  # Fix ==> WARNING: Package contains reference to $srcdir
  # gost.so.1.1 contain path to source files
  # strings gost.so.1.1 | grep "src\/gost-engine"
  # Show containing strings
  sed -i "$ a\ \nset(CMAKE_C_FLAGS \"\${CMAKE_C_FLAGS} -fdebug-prefix-map=\\\\\"\${CMAKE_SOURCE_DIR}\\\\\"=. -Wno-builtin-macro-redefined -D'__FILE__=\\\\\"\$(subst \$(realpath \${CMAKE_SOURCE_DIR})/,,\$(abspath \$<))\\\\\"'\")" \
    "${srcdir:?}/${pkgname:?}/CMakeLists.txt"

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S gost-engine \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOPENSSL_ROOT_DIR=/usr \
    -DOPENSSL_INCLUDE_DIR=/usr/include \
    -DOPENSSL_LIBRARIES=/usr/lib \
    -DOPENSSL_ENGINES_DIR=/usr/lib/engines-1.1 \
    -DCMAKE_BUILD_TYPE=Release

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build
  install -Dm644 "${srcdir:?}/gost.cnf" "${pkgdir:?}/etc/ssl/gost.cnf"
  install -Dm644 "${srcdir:?}/${pkgname:?}/LICENSE" "${pkgdir:?}/usr/share/licenses/${pkgname:?}/LICENSE"

}
