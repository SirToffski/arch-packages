pkgname=kwayland-server+clang
pkgver=5.22.2.1
pkgrel=1
pkgdesc='Wayland server components built on KDE Frameworks'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
provides=("kwayland-server" "kwayland-server=${pkgver}")
conflicts=("kwayland-server")
depends=(qt5-base)
makedepends=(extra-cmake-modules plasma-wayland-protocols wayland-protocols kwayland doxygen qt5-tools)
groups=(plasma)
source=(
    "https://download.kde.org/stable/plasma/${pkgver}/kwayland-server-${pkgver}.tar.xz"{,.sig}
    'lto.cmake'
)
sha256sums=('8d71bf529572897f4b3d76f6735bf4799176e6ccd2f4eeb58e5c58fb117325e6'
    'SKIP'
    '7971e5dd106804e615bf270a1d660835ded44f5707263e40a0b402cd1d63348e')
validpgpkeys=(
    '2D1D5B0588357787DE9EE225EC94D18F7F05997E' # Jonathan Riddell <jr@jriddell.org>
    '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D' # Bhushan Shah <bshah@kde.org>
    'D07BD8662C56CB291B316EB2F5675605C74E02CF' # David Edmundson <davidedmundson@kde.org>
    '1FA881591C26B276D7A5518EEAAF29B42A678C20' # Marco Martin <notmart@gmail.com>
)

_llvm=0

prepare() {

    if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
        _llvm=1
    fi

}

build() {

    if [[ ${_llvm} -eq 1 ]]; then

        cmake -B build -S kwayland-server-${pkgver} \
            -DCMAKE_TOOLCHAIN_FILE=../lto.cmake \
            -DBUILD_TESTING=OFF \
            -DBUILD_QCH=ON

    else

        cmake -B build -S kwayland-server-${pkgver} \
            -DBUILD_TESTING=OFF \
            -DBUILD_QCH=ON

    fi

    cmake --build build

}

package() {

    DESTDIR="${pkgdir:?}" cmake --install build

}
