pkgname=libxext+clang
pkgver=1.3.4
pkgrel=3
pkgdesc="X11 miscellaneous extensions library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
provides=(libxext "libxext=${pkgver}")
conflicts=(libxext)
depends=('libx11')
makedepends=('xorg-util-macros' 'xorgproto')
source=("${url}/releases/individual/lib/libXext-${pkgver}.tar.bz2"{,.sig})
sha512sums=('09146397d95f80c04701be1cc0a9c580ab5a085842ac31d17dfb6d4c2e42b4253b89cba695e54444e520be359883a76ffd02f42484c9e2ba2c33a5a40c29df4a'
  'SKIP')
validpgpkeys=(
  '3C2C43D9447D5938EF4551EBE23B7E70B467F0BF' # "Peter Hutterer (Who-T) <office@who-t.net>"
  '4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E' # Alan Coopersmith <alan.coopersmith@oracle.com>
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libXext-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libXext-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" \
      ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --disable-static

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" make

  else

    ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --disable-static

    make

  fi

}

package() {

  cd "${srcdir:?}/libXext-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libXext-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  install -m755 -d "${pkgdir:?}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
