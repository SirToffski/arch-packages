pkgname=xz+clang
pkgver=5.2.5
pkgrel=3
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('x86_64')
url='https://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
provides=(xz "xz=${pkgver}")
conflicts=(xz)
depends=('sh')
source=(
  "https://tukaani.org/xz/xz-${pkgver}.tar.gz"{,.sig}
  https://tukaani.org/xz/xzgrep-ZDI-CAN-16587.patch
)
sha256sums=('f6f4910fd033078738bd82bfba4f49219d03b17eb0794eb91efbae419f4aba10'
  'SKIP'
  '98c6cb1042284fe704ec30083f3fc87364ce9ed2ea51f62bbb0ee9d3448717ec')
validpgpkeys=('3690C240CE51B4670D30AD1C38EE757D69184620')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  patch -d "xz-${pkgver}" -p1 -i "${srcdir:?}/xzgrep-ZDI-CAN-16587.patch"

}

build() {

  cd "${srcdir:?}/xz-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/xz-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin -Wno-format-nonliteral"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  else

    CFLAGS="${CFLAGS} -Wno-format-truncation"

  fi

  ./configure --prefix=/usr \
    --disable-rpath \
    --enable-werror

  make

}

check() {

  make check -C "xz-${pkgver}"

}

package() {

  make DESTDIR="${pkgdir:?}" install -C "xz-${pkgver}"

  install -d -m755 "${pkgdir:?}/usr/share/licenses/xz/"
  ln -sf /usr/share/doc/xz/COPYING "${pkgdir:?}/usr/share/licenses/xz/"
  ln -sf /usr/share/licenses/common/GPL2/license.txt "${pkgdir:?}/usr/share/doc/xz/COPYING.GPLv2"

}
