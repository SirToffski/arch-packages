pkgname=jq+clang
pkgver=1.6
pkgrel=4
pkgdesc='Command-line JSON processor'
arch=('x86_64')
url='https://stedolan.github.io/jq/'
license=('MIT')
provides=('jq' "jq=${pkgver}")
conflicts=('jq')
depends=('glibc' 'oniguruma')
makedepends=('autoconf' 'automake' 'bison' 'flex' 'python')
source=("https://github.com/stedolan/jq/releases/download/jq-${pkgver}/jq-${pkgver}.tar.gz")
sha256sums=('5de8c8e29aaa3fb9cc6b47bb27299f271354ebb72514e3accadc7d38b5bbaa72')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/jq-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/jq-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr

  make

}

package() {

  cd "${srcdir:?}/jq-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/jq-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" prefix=/usr install
  install -Dm644 COPYING "${pkgdir:?}/usr/share/licenses/jq/COPYING"

}
