pkgname=virglrenderer+clang
pkgver=0.9.1
pkgrel=1
pkgdesc='A virtual 3D GPU library, that allows the guest operating system to use the host GPU to accelerate 3D rendering'
arch=(x86_64)
url='https://virgil3d.github.io/'
license=(MIT)
provides=('virglrenderer' "virglrenderer=${pkgver}")
conflicts=('virglrenderer')
depends=(libepoxy mesa)
makedepends=(python meson)
checkdepends=(check)
_tag=virglrenderer-${pkgver}
source=("virglrenderer-${pkgver}.tar.bz2::https://gitlab.freedesktop.org/virgl/virglrenderer/-/archive/${_tag}/virglrenderer-${_tag}.tar.bz2")
sha256sums=('860cc3f0f1a448542a812b6e13909824eb3140b7b3283d068e9640487d50e6c0')

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

build() {

  meson setup "virglrenderer-${_tag}" build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true

  meson compile -C build

}

check() {

  meson test -C build --print-errorlogs

}

package() {

  meson install -C build --destdir="${pkgdir:?}"

  install -D -m644 "virglrenderer-${_tag}/COPYING" "${pkgdir:?}/usr/share/licenses/virglrenderer/COPYING"

}
