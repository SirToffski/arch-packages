pkgname=xcb-util-keysyms+clang
pkgver=0.4.0
pkgrel=3
pkgdesc="Utility libraries for XC Binding - Standard X key constants and conversion to/from keycodes"
arch=('x86_64')
url="https://xcb.freedesktop.org"
license=('custom')
provides=("xcb-util-keysyms" "xcb-util-keysyms=${pkgver}")
conflicts=("xcb-util-keysyms")
depends=('libxcb>=1.7')
makedepends=('xorg-util-macros' 'xorgproto')
source=("https://xcb.freedesktop.org/dist/xcb-util-keysyms-${pkgver}.tar.bz2"
  'LICENSE')
sha512sums=('b14b3100c3ae2379f9df799c0780a1ee39267101e58e4c1c6f390f039348ca2b370bb1df203b9bc68a170d664188d9c73e3553a7ba24d98abea3ed353e8dc0c4'
  '62f83468f9d9503a5a8c1ebaac78cbaefe3c485f8bfd7192817ec59254872fe2914ed66ae8b9e7f7a6ef5af283048c11390f44eb8f0f70b1c136ef5c75282e8c')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/xcb-util-keysyms-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/xcb-util-keysyms-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"

  fi

  ./configure \
    --prefix=/usr \
    --disable-static

  make

}

check() {

  cd "${srcdir:?}/xcb-util-keysyms-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/xcb-util-keysyms-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  make check

}

package() {

  cd "${srcdir:?}/xcb-util-keysyms-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/xcb-util-keysyms-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  install -D -m644 "${srcdir:?}/LICENSE" \
    "${pkgdir:?}/usr/share/licenses/${pkgname}/LICENSE"
}
