pkgname=libfontenc+clang
pkgver=1.1.4
pkgrel=3
pkgdesc="X11 font encoding library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
provides=('libfontenc' "libfontenc=${pkgver}")
conflicts=('libfontenc')
depends=('zlib' 'xorg-fonts-encodings')
makedepends=('pkgconfig' 'xorgproto' 'xorg-util-macros')
source=("${url}/releases/individual/lib/libfontenc-${pkgver}.tar.bz2"{,.sig})
sha512sums=('76fa851d00113241f15fdd5b5bb7e927b8d8b9a82ce3fcb0678b8c7e32cb5e8d898c4bda1d60dafb7748145049d3240627ac34a2360c64e98a7a912ea7c30582'
  'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libfontenc-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libfontenc-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --with-encodingsdir=/usr/share/fonts/encodings

  make

}

package() {

  cd "${srcdir:?}/libfontenc-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libfontenc-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  install -Dm644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/COPYING"

}
