pkgname=attr+clang
pkgver=2.5.1
pkgrel=2
pkgdesc='Extended attribute support library for ACL support'
arch=('x86_64')
url='https://savannah.nongnu.org/projects/attr'
license=('LGPL')
depends=('glibc')
makedepends=('gettext')
replaces=('xfsattr')
provides=('xfsattr' 'libattr.so' 'attr' "attr=${pkgver}")
conflicts=('xfsattr' 'attr')
options=('debug')
backup=('etc/xattr.conf')
validpgpkeys=('600CD204FBCEA418BD2CA74F154343260542DF34' # Brandon Philips <brandon@ifup.co>
  'B902B5271325F892AC251AD441633B9FE837F581'             # Frysinger <vapier@gentoo.org>
  '259B3792B3D6D319212CC4DCD5BF9FEB0313653A')            # Andreas Gruenbacher <andreas.gruenbacher@gmail.com>
source=("https://download.savannah.gnu.org/releases/attr/attr-${pkgver}.tar.xz"{,.sig})
sha256sums=('db448a626f9313a1a970d636767316a8da32aede70518b8050fa0de7947adc32'
  'SKIP')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cd "${srcdir:?}/attr-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/attr-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  ./configure \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --prefix=/usr \
    --sysconfdir=/etc

  make

}

package() {

  make DESTDIR="${pkgdir:?}" install -C "attr-${pkgver}"

}
