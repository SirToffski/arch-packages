pkgname=swaylock+clang
pkgver=1.5
pkgrel=3
license=("MIT")
pkgdesc="Screen locker for Wayland"
makedepends=(
  'meson'
  'scdoc'
  'wayland-protocols'
  'git' # To make the version string
)
depends=(
  'cairo'
  'libxkbcommon'
  'pam'
  'wayland'
  'gdk-pixbuf2' # For images other than PNG
)
provides=('swaylock' "swaylock=${pkgver}")
conflicts=('swaylock')
arch=('x86_64')
url="https://github.com/swaywm/swaylock"
source=(
  "swaylock-$pkgver.tar.gz::https://github.com/swaywm/swaylock/archive/$pkgver.tar.gz"
  "https://github.com/swaywm/swaylock/releases/download/$pkgver/swaylock-$pkgver.tar.gz.sig"
)
sha256sums=('37b9c70e5698e8872b0b964a7454ecdb0f1a9e06b686eecf2b6c54f7f64a3f42'
  'SKIP')
validpgpkeys=("9DDA3B9FA5D58DD5392C78E652CB6609B22DA89A") # Drew DeVault

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}


build() {

  meson setup "swaylock-${pkgver}" build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true

  meson compile -C build

}

package() {

  DESTDIR="${pkgdir:?}" meson install -C build

  install -Dm644 "swaylock-$pkgver/LICENSE" -t "${pkgdir:?}/usr/share/licenses/swaylock"
  install -Dm644 "swaylock-$pkgver/README.md" -t "${pkgdir:?}/usr/share/doc/swaylock"

}
