pkgname=mesa+clang
pkgdesc="Mesa open-source implementation of the OpenGL specification"
pkgver=21.1.4
_pkgver=21.1.4
pkgrel=1
arch=('x86_64')
makedepends=('git' 'python-mako' 'xorgproto'
  'libxml2' 'libx11' 'libvdpau' 'libva' 'elfutils' 'libxrandr'
  'ocl-icd' 'wayland-protocols' 'meson' 'ninja' 'glslang' 'valgrind')
depends=('libdrm' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'libelf'
  'libomxil-bellagio' 'libunwind' 'libglvnd' 'wayland' 'lm_sensors' 'libclc' 'vulkan-icd-loader' 'zstd' 'expat')
optdepends=('opengl-man-pages: for the OpenGL API man pages')
provides=('mesa' "mesa=${pkgver}" 'opencl-mesa' 'vulkan-intel' 'vulkan-radeon' 'vulkan-mesa-layer' 'libva-mesa-driver' 'mesa-vdpau' 'vulkan-swrast' 'vulkan-driver' 'opengl-driver' 'opencl-driver' 'mesa-libgl')
conflicts=('mesa' 'opencl-mesa' 'vulkan-intel' 'vulkan-radeon' 'vulkan-mesa-layer' 'libva-mesa-driver' 'mesa-vdpau' 'vulkan-swrast')
url="https://www.mesa3d.org"
license=('custom')
source=(
  "https://archive.mesa3d.org/mesa-${_pkgver}.tar.xz"{,.sig}
  'LICENSE'
  'mesa.conf'
  '0001.patch'
  '0002.patch'
)

md5sums=('645a27655bea5d83752da49510836731'
  'SKIP'
  '5c65a0fe315dd347e09b1f2826a1df5a'
  '79e9d277704ee3815ac03d22bcf5da0d'
  '3aa4b1f30be9eecead2251fb33049b99'
  '888ebe9c1f510fae5a1c6b1a62c41a2f')
sha512sums=('1f4e2d8ba2c3c02bad5468687752d7e87d6e79c4a0e6ec4b2741a57fb270d18ae4712aaa07a9e09a2d3348540f5a26cdbe05464cd52adb8df1e2e28a581b2b53'
  'SKIP'
  '25da77914dded10c1f432ebcbf29941124138824ceecaf1367b3deedafaecabc082d463abcfa3d15abff59f177491472b505bcb5ba0c4a51bb6b93b4721a23c2'
  'd4434797c3d8da997ba9db96f11cd1f0c0a76a71ee6779955801e1fb6c6221f8aa1a6779047ea27660d4b37696035adea926b5d0ef260447f3a4b7a3148ed7e0'
  '0659dc54411fac163322f6d6770236de6a1742f7c362f57c25e626395cf6011d9679ddce97ff6e7a1c202e32cf11df19517bd71fac75a97939cafa57deaccafc'
  '9e4270ff285ad7eeeefaebc512c707d4d11fce63272b837e8f26f761e1d499b732b08d2dac8ace818a3d80c9614a39d14e4e19fb468164ec0fa61c757fa08ca7')

validpgpkeys=(
  '57551DE15B968F6341C248F68D8E31AFC32428A6' # Keith Packard <keithp@keithp.com>
)

# NINJAFLAGS is an env var used to pass commandline options to ninja
# NOTE: It's your responbility to validate the value of $NINJAFLAGS. If unsure, don't set it.

DRI_DRIVERS="i915,i965,r200,r100,nouveau"
GALLIUM_DRIVERS="r300,r600,radeonsi,nouveau,svga,swrast,virgl,iris,zink"
VULKAN_DRIVERS="amd,intel,swrast"

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

  if [[ -d _build ]]; then
    rm -rf _build
  fi

  cd "${srcdir:?}/mesa-${_pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/mesa-${_pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  # Fix radeonsi rendering glitches
  patch --forward --strip=1 --input="${srcdir}/0001.patch"
  patch --forward --strip=1 --input="${srcdir}/0002.patch"

  if [[ -f "${srcdir:?}/mesa.conf" ]]; then
    # shellcheck disable=SC1091
    source "${srcdir:?}/mesa.conf"
  fi

}

build() {

  meson setup mesa-${_pkgver} _build \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D prefix=/usr \
    -D sysconfdir=/etc \
    -D platforms=x11,wayland \
    -D dri-drivers="${DRI_DRIVERS}" \
    -D gallium-drivers="${GALLIUM_DRIVERS}" \
    -D vulkan-drivers="${VULKAN_DRIVERS}" \
    -D gallium-extra-hud=true \
    -D gallium-omx=bellagio \
    -D gallium-nine=true \
    -D gallium-va=enabled \
    -D gallium-vdpau=enabled \
    -D gallium-xa=enabled \
    -D gallium-xvmc=disabled \
    -D llvm=enabled \
    -D dri3=enabled \
    -D egl=enabled \
    -D gbm=enabled \
    -D gles1=disabled \
    -D gles2=enabled \
    -D glvnd=true \
    -D glx=dri \
    -D libunwind=enabled \
    -D lmsensors=enabled \
    -D osmesa=true \
    -D shared-glapi=enabled \
    -D gallium-opencl=icd \
    -D vulkan-layers=device-select,overlay \
    -D zstd=enabled \
    -D tools=[] \
    -D microsoft-clc=disabled \
    -D valgrind=disabled

  meson configure _build

  ninja ${NINJAFLAGS} -C _build

}

package() {

  DESTDIR="${pkgdir:?}" ninja ${NINJAFLAGS} -C _build install

  rm -f "${pkgdir}/usr/bin/mesa-overlay-control.py"
  rm -rf "${pkgdir:?}/usr/bin"

  ln -s /usr/lib/libGLX_mesa.so.0 "${pkgdir:?}/usr/lib/libGLX_indirect.so.0"

  install -Dm644 "${srcdir:?}/LICENSE" "${pkgdir:?}/usr/share/licenses/${pkgbase:?}/LICENSE"

}
