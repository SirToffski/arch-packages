pkgname=mesa+clang
pkgdesc="Mesa open-source implementation of the OpenGL specification"
pkgver=21.3.1
pkgrel=2
arch=('x86_64')
makedepends=('git' 'python-mako' 'xorgproto' 'llvm'
  'libxml2' 'libx11' 'libvdpau' 'libva' 'elfutils' 'libxrandr'
  'ocl-icd' 'wayland-protocols' 'meson' 'ninja' 'glslang' 'valgrind')
depends=('libdrm' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'libelf'
  'libomxil-bellagio' 'libunwind' 'libglvnd' 'wayland' 'lm_sensors' 'libclc' 'vulkan-icd-loader' 'zstd' 'expat')
optdepends=('opengl-man-pages: for the OpenGL API man pages')
provides=('mesa' "mesa=${pkgver}" 'opencl-mesa' 'vulkan-intel' 'vulkan-radeon' 'vulkan-mesa-layer' 'libva-mesa-driver' 'mesa-vdpau' 'vulkan-swrast' 'vulkan-driver' 'opengl-driver' 'opencl-driver' 'mesa-libgl')
conflicts=('mesa' 'opencl-mesa' 'vulkan-intel' 'vulkan-radeon' 'vulkan-mesa-layer' 'libva-mesa-driver' 'mesa-vdpau' 'vulkan-swrast')
url="https://www.mesa3d.org"
license=('custom')
source=(
  "https://archive.mesa3d.org/mesa-${pkgver}.tar.xz"{,.sig}
  'LICENSE'
  'mesa.conf'
  'mesa-remove-swr.patch'
)

sha256sums=('2b0dc2540cb192525741d00f706dbc4586349185dafc65729c7fda0800cc474d'
  'SKIP'
  '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2'
  '7efdb0171d5346f856ce1b2ff440fc0810938d4dd2ad196eed9c25143f9b4c63'
  '078418721cf7fdae71bf05c8d979649925bebd026e2ecaf0c615d1133a1ce726')

validpgpkeys=(
  '57551DE15B968F6341C248F68D8E31AFC32428A6' # Keith Packard <keithp@keithp.com>
  '71C4B75620BC75708B4BDB254C95FAAB3EB073EC' # Dylan Baker <dylan@pnwbakers.com>
)

DRI_DRIVERS="i915,i965,r200,r100,nouveau"
GALLIUM_DRIVERS="r300,r600,radeonsi,nouveau,virgl,svga,swrast,iris,zink,crocus"
VULKAN_DRIVERS="amd,intel,swrast"
VULKAN_LAYERS="device-select,intel-nullhw,overlay"

_extra_lto_flag=""

_patch() {

  echo -e "\E[1;33m Apply patch: ${1}\E[0m"
  patch -p1 -i "${srcdir:?}/${1}"

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

  cd "${srcdir:?}/mesa-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/mesa-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  # Remove SWR driver. SWR already removed from main branch.
  _patch mesa-remove-swr.patch

  if [[ -f "${srcdir:?}/mesa.conf" ]]; then
    # shellcheck disable=SC1091
    source "${srcdir:?}/mesa.conf"
  fi

}

build() {

  meson setup mesa-${pkgver} build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D sysconfdir=/etc \
    -D platforms=x11,wayland \
    -D dri-drivers="${DRI_DRIVERS}" \
    -D gallium-drivers="${GALLIUM_DRIVERS}" \
    -D vulkan-drivers="${VULKAN_DRIVERS}" \
    -D vulkan-layers="${VULKAN_LAYERS}" \
    -D gallium-extra-hud=true \
    -D gallium-omx=bellagio \
    -D gallium-nine=true \
    -D gallium-opencl=icd \
    -D gallium-va=enabled \
    -D gallium-vdpau=enabled \
    -D gallium-xa=enabled \
    -D gallium-xvmc=disabled \
    -D llvm=enabled \
    -D dri3=enabled \
    -D egl=enabled \
    -D gbm=enabled \
    -D gles1=disabled \
    -D gles2=enabled \
    -D glvnd=true \
    -D glx=dri \
    -D libunwind=enabled \
    -D lmsensors=enabled \
    -D osmesa=true \
    -D shared-glapi=enabled \
    -D zstd=enabled \
    -D microsoft-clc=disabled \
    -D valgrind=disabled

  meson compile -C build

}

package() {

  DESTDIR="${pkgdir:?}" meson install -C build

  ln -s /usr/lib/libGLX_mesa.so.0 "${pkgdir:?}/usr/lib/libGLX_indirect.so.0"

  install -Dm644 "${srcdir:?}/LICENSE" "${pkgdir:?}/usr/share/licenses/${pkgbase:?}/LICENSE"

}
