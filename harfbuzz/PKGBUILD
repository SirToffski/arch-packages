pkgbase=harfbuzz-noname
pkgname=(harfbuzz-noname harfbuzz-icu-noname)
pkgver=2.8.1
pkgrel=1
pkgdesc="OpenType text shaping engine"
url="https://www.freedesktop.org/wiki/Software/HarfBuzz"
arch=(x86_64)
license=(MIT)
makedepends=(glib2 freetype2 graphite cairo icu gobject-introspection gtk-doc
  ragel git python meson chafa)
checkdepends=(python-fonttools python-setuptools)
_commit=b37f03f16b39d397a626f097858e9ae550234ca0 # tags/2.8.1^0
source=("git+https://github.com/harfbuzz/harfbuzz#commit=$_commit")
sha256sums=('SKIP')

pkgver() {

  cd "${srcdir:?}/harfbuzz" || (
    echo -e "\E[1;31mCan't cd to {srcdir:?}/harfbuzz directory! PkgVer Failed! \E[0m"
    exit 1
  )

  git describe --tags | sed 's/-/+/g'

}

prepare(){

  sed -i "s/'check-static-inits',//" harfbuzz/src/meson.build

}


build() {

  meson harfbuzz build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode=nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D b_pie=true \
    -D glib=enabled \
    -D gobject=enabled \
    -D cairo=enabled \
    -D icu=enabled \
    -D graphite=enabled \
    -D freetype=enabled \
    -D chafa=enabled \
    -D introspection=enabled

  meson compile -C build

}

check() {

  meson test -C build --print-errorlogs

}

package_harfbuzz-noname() {
  depends=('glib2' 'freetype2' 'graphite' 'libglib-2.0.so' 'libfreetype.so'
    'libgobject-2.0.so')
  provides=('harfbuzz' 'libharfbuzz.so' 'libharfbuzz-subset.so' 'libharfbuzz-gobject.so')
  conflicts=('harfbuzz')
  optdepends=('cairo: hb-view program'
    'chafa: hb-view program')

  DESTDIR="${pkgdir:?}" meson install -C build

  # Split harfbuzz-icu
  mkdir -p hb-icu/usr/{include/harfbuzz,lib/pkgconfig}
  mv -t hb-icu/usr/lib "${pkgdir:?}/usr/lib/libharfbuzz-icu"*
  mv -t hb-icu/usr/lib/pkgconfig "${pkgdir:?}/usr/lib/pkgconfig/harfbuzz-icu.pc"
  mv -t hb-icu/usr/include/harfbuzz "${pkgdir:?}/usr/include/harfbuzz/hb-icu.h"

  install -Dt "${pkgdir:?}/usr/share/licenses/${pkgname:?}" -m644 harfbuzz/COPYING

}

package_harfbuzz-icu-noname() {

  pkgdesc="${pkgdesc} (ICU integration)"
  depends=("harfbuzz-noname=${pkgver}-${pkgrel}" 'icu' 'libharfbuzz.so')
  provides=('harfbuzz-icu' 'libharfbuzz-icu.so')
  conflicts=('harfbuzz-icu')

  mv hb-icu/* "${pkgdir:?}"

  install -Dt "${pkgdir:?}/usr/share/licenses/${pkgname:?}" -m644 harfbuzz/COPYING

}
