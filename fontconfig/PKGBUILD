pkgname=fontconfig+clang
pkgver=2.13.94
pkgrel=7
epoch=2
pkgdesc="Library for configuring and customizing font access"
url=https://www.freedesktop.org/wiki/Software/fontconfig/
arch=(x86_64)
license=(custom)
depends=(libxml2 libfreetype.so)
makedepends=(git meson gperf freetype2
  docbook-utils docbook-sgml perl-sgmls)
provides=(fontconfig "fontconfig=${pkgver}" libfontconfig.so fontconfig-docs)
conflicts=(fontconfig fontconfig-docs)
replaces=('fontconfig-docs<2:2.13.93-1')
install=fontconfig.install
backup=(etc/fonts/fonts.conf)
_commit=e474b380ffdb330eca7dd0f0fddacc7b19f3247d
source=(
  #"git+https://gitlab.freedesktop.org/fontconfig/fontconfig.git#commit=${_commit}"
  "https://gitlab.freedesktop.org/fontconfig/fontconfig/-/archive/${_commit}/fontconfig-${_commit}.tar.gz"
  40-fontconfig-config.script
  40-fontconfig-config.hook
  fontconfig.hook
  fontconfig-sleep-less.patch
  fontconfig-meson-add-libxml2-parser.patch
  fontconfig-improvements.patch
)

sha256sums=('ba33e4aad2d7ed7d489f69d0a83e62ceab2b7b181123e67c1874fcdbde134b31'
  '7a9d50bccc709eb15db6ba8e13f69bc9d79b0bf354f1d17c1a5b2748edff3c33'
  '44f12491c9fd7eff825853846a2b3b8df2b96fc6520be5cb31ce7f37a160ff02'
  'fd7b6ce8ce178107f2e0b52462ebf186b6051c6eec945770107fda57048c9f34'
  'b2bee567aab161615cf565a5a426ab2e93ab706f0913d4a3ff94b8f40766b574'
  'bbbaf032d175b339fd39647e342d67455cd4a0df5f9435a918f3e758291fb0e6'
  '1a36bab2704ea292a75e0571d130e4ca0ffdb496b9f5c548a86e31317e176218')

_extra_lto_flag=""

_patch() {

  echo -e "\E[1;33m Apply patch: ${1}\E[0m"
  patch -p1 -i "${srcdir:?}/${1}"

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

  cd "${srcdir:?}/fontconfig-${_commit}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/fontconfig-${_commit} directory! Prepare Failed! \E[0m"
    exit 1
  )

  _patch fontconfig-sleep-less.patch
  _patch fontconfig-meson-add-libxml2-parser.patch
  _patch fontconfig-improvements.patch

}

build() {

  meson setup "fontconfig-${_commit}" build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D doc-txt=disabled \
    -D doc-pdf=disabled \
    -D doc-html=enabled \
    -D cache-build=disabled

  meson compile -C build

}

check() {

  meson test -C build --print-errorlogs

}

package() {

  meson install -C build --destdir "${pkgdir:?}"

  # Handle conf.d using the hook to avoid overwriting the symlinks on upgrade
  mkdir -p "${pkgdir:?}/usr/share/fontconfig/conf.default"
  for _f in "${pkgdir:?}/etc/fonts/conf.d/"*.conf; do
    ln -sr "${pkgdir:?}/usr/share/fontconfig/conf."{avail,default}"/${_f##*/}"
    rm "${_f}"
  done

  install -Dt "${pkgdir:?}/usr/share/libalpm/hooks" -m644 {40-fontconfig-config,fontconfig}.hook
  install -D 40-fontconfig-config.script \
    "${pkgdir:?}/usr/share/libalpm/scripts/40-fontconfig-config"
  install -Dt "${pkgdir:?}/usr/share/licenses/${pkgname}" -m644 "fontconfig-${_commit}/COPYING"

}
