pkgname=cairo+clang
pkgver=1.17.6
pkgrel=2
pkgdesc="2D graphics library with support for multiple output devices"
url="https://cairographics.org/"
arch=(x86_64)
license=(LGPL MPL)
provides=('cairo' "cairo=${pkgver}")
conflicts=('cairo')
depends=(lzo zlib libpng fontconfig freetype2 libx11 libxext libxrender libxcb
  glib2 pixman)
makedepends=(valgrind git meson gtk-doc)
_commit=b43e7c6f3cf7855e16170a06d3a9c7234c60ca94 # tags/1.17.6^0
source=(
  "git+https://gitlab.freedesktop.org/cairo/cairo.git#commit=${_commit}"
  0001-Fix-type1-subset-indexing.patch
)
sha256sums=('SKIP'
  '296be3c73638314bea08fa51b5f1650ea0a2aab2a037ea55e41c319d64ca4c3c')

_extra_lto_flag=""

pkgver() {

  cd "${srcdir:?}/cairo" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/cairo directory! PkgVer Failed! \E[0m"
    exit 1
  )

  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

  cd "${srcdir:?}/cairo" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/cairo! Prepare Failed! \E[0m"
    exit 1
  )

  # FS#74354
  patch -p1 -i "${srcdir:?}/0001-Fix-type1-subset-indexing.patch"

  # Set default LCD filter to FreeType's default
  # https://bugzilla.redhat.com/show_bug.cgi?id=1645763
  git cherry-pick -n 2f118e79df74942cd959b783bd81ec0e3da8d4b3

}

build() {

  meson setup cairo build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D spectre=disabled \
    -D tee=enabled \
    -D tests=disabled \
    -D symbol-lookup=disabled \
    -D gtk_doc=true

  meson compile -C build

}

package() {

  meson install -C build --destdir "${pkgdir:?}"

}
