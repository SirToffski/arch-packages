pkgname=xorg-font-util+clang
pkgver=1.3.2
pkgrel=2
pkgdesc='X.Org font utilities'
url='https://xorg.freedesktop.org/'
arch=('x86_64')
license=('custom')
provides=('font-util' "xorg-font-util" "xorg-font-util=${pkgver}")
conflicts=('font-util' "xorg-font-util")
makedepends=('xorg-util-macros')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E')
source=("https://xorg.freedesktop.org/releases/individual/font/font-util-${pkgver}.tar.bz2"{,.sig})
sha512sums=(
  '7cac529b12ae71185b89c72c1569b9826f52eeaecc1c76010338e7c42c4078ae339f18220b580bbd68fb5dc09df6ecf169a47c32e6104d8ee53bd443fa21d167'
  'SKIP'
)

groups=('xorg-fonts' 'xorg')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi
}

build() {

  cd "${srcdir:?}/font-util-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/font-util-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --with-mapdir=/usr/share/fonts/util \
    --with-fontrootdir=/usr/share/fonts

  make

}

package() {

  cd "${srcdir:?}/font-util-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/font-util-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  install -Dm644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/COPYING"

}
