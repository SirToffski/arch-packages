pkgname=libxxf86vm+clang
pkgver=1.1.4
pkgrel=4
pkgdesc="X11 XFree86 video mode extension library"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
provides=(libxxf86vm)
conflicts=(libxxf86vm)
depends=('libxext')
makedepends=('xorg-util-macros' 'xorgproto')
source=("${url}/releases/individual/lib/libXxf86vm-${pkgver}.tar.bz2"{,.sig})
sha512sums=('c5f92d86e143db02ebb36bcd25618acaa2cb2831f5a23800a06dd431cd73b6702d95de8fe7407ce626336bf614c288d5256f4d87ea7781fad2ab6c517cbf09e0'
  'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libXxf86vm-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libXxf86vm-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" \
      ./configure \
      --prefix=/usr \
      --disable-static

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" make

  else

    ./configure \
      --prefix=/usr \
      --disable-static

    make

  fi

}

package() {

  cd "${srcdir:?}/libXxf86vm-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libXxf86vm-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

}
