pkgname=phonon-qt5-gstreamer+clang
pkgver=4.10.0
pkgrel=3
arch=(x86_64)
pkgdesc="Phonon GStreamer backend for Qt5"
url='https://community.kde.org/Phonon'
license=(LGPL)
provides=("phonon-qt5-gstreamer" "phonon-qt5-gstreamer=${pkgver}" phonon-qt5-backend)
conflicts=("phonon-qt5-gstreamer")
depends=(gst-plugins-base qt5-x11extras phonon-qt5)
optdepends=('pulseaudio: PulseAudio support' 'gst-plugins-good: PulseAudio support and good codecs' 'gst-plugins-bad: additional codecs'
  'gst-plugins-ugly: additional codecs' 'gst-libav: libav codec')
makedepends=(extra-cmake-modules qt5-tools)
source=(
  "https://download.kde.org/stable/phonon/phonon-backend-gstreamer/${pkgver}/phonon-backend-gstreamer-${pkgver}.tar.xz"
)
sha256sums=('c5871835521d015ef2ad1276b1f58340d946c2903466337f3170bac3c58d61f2')
validpgpkeys=(CB9387521E1EE0127DA804843FDBB55084CC5D84) # Harald Sitter <sitter@kde.org>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S phonon-backend-gstreamer-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
