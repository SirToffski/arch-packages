pkgname=ndctl+clang
pkgver=73
pkgrel=1
pkgdesc='Utility library for managing the libnvdimm (non-volatile memory device) sub-system in the Linux kernel'
arch=('x86_64')
url="https://github.com/pmem/ndctl"
license=('LGPL')
provides=("ndctl" "ndctl=${pkgver}")
conflicts=(ndctl)
depends=('iniparser' 'json-c' 'keyutils' 'kmod' 'systemd-libs' 'util-linux-libs')
makedepends=('meson' 'asciidoctor' 'bash-completion' 'systemd' 'xmlto')
source=("https://github.com/pmem/ndctl/archive/v${pkgver}/ndctl-${pkgver}.tar.gz")
sha256sums=('97f85b897378d293da75f91c2f238c0a45e1626dc2c2367e7a8d78378ee0f73b')

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

  # fix location of modprobe.d conf
  sed -i "s|^modprobedatadir =.*|modprobedatadir = '/usr/lib/modprobe.d/'|" "ndctl-${pkgver}/contrib/meson.build"

}

build() {

  meson setup "ndctl-${pkgver}" build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true

  meson compile -C build

}

check() {

  meson test -C build --print-errorlogs

}

package() {

  meson install -C build --destdir "${pkgdir:?}"

}
